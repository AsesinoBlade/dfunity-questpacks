Quest: JHEEKN01
DisplayName: The Might of Ogres
-- Message panels
QRC:

QuestorOffer:   [1000]
<ce>  One of %crn's more corrupt nobles has sicced
<ce>  a pair of ogres on an old widow's property,
<ce>  hoping to get her to surrender it into his
<ce>  hands without proper payment.
<ce>  We would be ashamed to let this matter
<ce>  slip us by. Will you slay the ogres and
<ce>  uphold the law of our land?

RefuseQuest:   [1001]
<ce>  Yes, we can go fetch her starved and
<ce>  desiccated corpse from the hovel afterward.
<ce>  Pathetic.

AcceptQuest:   [1002]
<ce>  I expected no less of you, %pct.
<ce>  Head to ___poorhome_, %di.
<ce>  The brutes are trained to monitor the property,
<ce>  so you won't have to wander in search of them.
<ce>  Report here with the deed done within
<ce>  =timer_ days, for we have legal proceedings to take
<ce>  against this corrupt baron once they are dead.

QuestFail:   [1003]
<ce>  You have not reported the dead ogres
<ce>  to _qgiver_ yet, and justice has been delayed.

QuestComplete:   [1004]
<ce>  Marvelous. This baron, this stain on civilization,
<ce>  will soon be behind bars or chiseling stone
<ce>  for the lowest possible bidder. We feared he could
<ce>  somehow tell the ogres to storm the property
<ce>  as a sort of deadman's switch, were we to proceed
<ce>  sooner than you acted. Feel proud for what you have done.

RumorsPostSuccess:   [1007]
Been seeing more ogres than usual lately.

Message:  1011
<ce>  You've arrived, but at the moment the ogres
<ce>  aren't in sight. However, their loud grunting
<ce>  alerts you to their presence.
<ce>  You've interrupted a short break they were taking.

Message:  1012
<ce>  The ogres are slain. Perhaps this widow's suffering
<ce>  at the hands of wicked robber-barons will end now.

Message:  1030
%qdt:
 I am headed to ___poorhome_,
 a hovel belonging to an old
 widow of ____poorhome_,
 to dispose of two ogres
 that stalk the property.
 I must slay them and return
 to __qgiver_ in ___qgiver_
 before =timer_ days have passed.

QBN:

Person _qgiver_ group Questor

Foe _enemy1_ is Ogre
Foe _enemy2_ is Ogre

Place _poorhome_ remote poorhome

Clock _timer_ 00:00 0 flag 1 range 0 2

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    pc at _poorhome_ do _arrive_

_arrive_ task:
    start task _spawn_

_spawn_ task:
    say 1011
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    create foe _enemy2_ every 0 minutes 1 times with 100% success
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true

_killed1_ task:
    killed _enemy1_

_killed2_ task:
    killed _enemy2_

_victory_ task:
    when _killed1_ and _killed2_
    say 1012

_qgclicked_ task:
  clicked npc _qgiver_

_clearclick_ task:
  when _qgclicked_ and not _victory_
  clear _qgclicked_ _clearclick_

_pcgetsnothing_ task:
  when _qgclicked_ and _victory_
  give pc nothing
  legal repute +2
  end quest

_timer_ task:
    say 1003
    end quest