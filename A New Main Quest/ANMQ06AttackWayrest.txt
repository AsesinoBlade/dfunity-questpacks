Quest: ANMQ06AttackWayrest
DisplayName: ANMQ06AttackWayrest
-- Message panels
QRC:

RumorsDuringQuest:   [1005]
All nations are attacking Wayrest City. Why is that?
<--->
Wayrest City is under attack.

Message:  1010
<ce>  A group of orcs are assaulting the city,
<ce>  hoping to gain the Totem for Orsinium.

Message:  1011
<ce>  Warriors from Sentinel are invading the city,
<ce>  hoping to gain the Totem for their nation.
<ce>  
<ce>  "For Akorithi and the sovereignty of Hammerfell!"
<--->
<ce>  Warriors from Daggerfall are invading the city,
<ce>  hoping to gain the Totem for their nation.
<ce>  
<ce>  "For King Gothryd, for Queen Aubk-i,
<ce>  and our fallen Lysandus! Charge!!"

Message:  1012
<ce>  A shambling horde of undead are assaulting
<ce>  the city. From their mouths you hear the words,
<ce>  "Totem... Totem... Totem..."

Message:  1013
<ce>  You see signs of a recent skirmish by the undead.
<--->
<ce>  You see signs of a recent skirmish by the soldiers of Sentinel.
<--->
<ce>  You see signs of a recent skirmish by the soldiers of Daggerfall.
<--->
<ce>  You see signs of a recent skirmish by orcs from Orsinium.

QBN:
Place _dagg_ permanent WayrestCity

Clock _refresh_ 2.00:00 4.00:00

Foe _hu1_ is Knight
Foe _hu2_ is Knight
Foe _hu3_ is Mage
Foe _hu4_ is Archer
Foe _hu5_ is Archer
Foe _hu6_ is Archer
Foe _orc1_ is Orc_sergeant
Foe _orc2_ is Orc_sergeant
Foe _orc3_ is Orc_shaman
Foe _orc4_ is Orc_warlord
Foe _und1_ is Zombie
Foe _und2_ is Zombie
Foe _und3_ is Skeletal_warrior
Foe _und4_ is Skeletal_warrior
Foe _und5_ is Ghost
Foe _und6_ is Wraith

--	Quest start-up:
    pc at _dagg_ do _skirmish_

_skirmish_ task:
    start timer _refresh_
    pick one of _human_ _dud_ _orc_ _dud_ _undead_ _dud_

_dud_ task:
    say 1013

_human_ task:
    say 1011
    create foe _hu1_ every 0 minutes 1 times with 100% success
    create foe _hu2_ every 0 minutes 1 times with 100% success
    create foe _hu3_ every 0 minutes 1 times with 100% success
    create foe _hu4_ every 0 minutes 1 times with 100% success
    create foe _hu5_ every 0 minutes 1 times with 100% success
    create foe _hu6_ every 0 minutes 1 times with 100% success
    change foe _hu1_ infighting true
    change foe _hu2_ infighting true
    change foe _hu3_ infighting true
    change foe _hu4_ infighting true
    change foe _hu5_ infighting true
    change foe _hu6_ infighting true
    change foe _hu1_ team 1
    change foe _hu2_ team 1
    change foe _hu3_ team 1
    change foe _hu4_ team 1
    change foe _hu5_ team 1
    change foe _hu6_ team 1

_orc_ task:
    say 1010
    create foe _orc1_ every 0 minutes 1 times with 100% success
    create foe _orc2_ every 0 minutes 1 times with 100% success
    create foe _orc3_ every 0 minutes 1 times with 100% success
    create foe _orc4_ every 0 minutes 1 times with 100% success
    change foe _orc1_ infighting true
    change foe _orc2_ infighting true
    change foe _orc3_ infighting true
    change foe _orc4_ infighting true
    change foe _orc1_ team 1
    change foe _orc2_ team 1
    change foe _orc3_ team 1
    change foe _orc4_ team 1

_undead_ task:
    say 1012
    create foe _und1_ every 0 minutes 1 times with 100% success
    create foe _und2_ every 0 minutes 1 times with 100% success
    create foe _und3_ every 0 minutes 1 times with 100% success
    create foe _und4_ every 0 minutes 1 times with 100% success
    create foe _und5_ every 0 minutes 1 times with 100% success
    create foe _und6_ every 0 minutes 1 times with 100% success
    change foe _und1_ infighting true
    change foe _und2_ infighting true
    change foe _und3_ infighting true
    change foe _und4_ infighting true
    change foe _und5_ infighting true
    change foe _und6_ infighting true

_refresh_ task:
    start quest ANMQ06AttackWayrest
    end quest