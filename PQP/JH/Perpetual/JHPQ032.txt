-- Perpetual Quest
Quest: JHPQ032
DisplayName: The Gravekeeper
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHPQ032

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHPQ032

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHPQ032

QuestFail:  [1003]
<ce>           ERROR 1003 JHPQ032

QuestComplete:  [1004]
<ce> ERROR 1004 JHPQ032

Message:  1010
<ce> Vampires often make use of cemeteries as places
<ce> of resort, including for clan meetings. The specimen
<ce> you see here could be preparing the way for others
<ce> or working alone. All the same, he begins to move.

Message:  1011
<ce> Your knowledge of Thaumaturgy allows you to get out a
<ce> quick cantrip to pacify the vampire. It won't hold forever,
<ce> but it gives you the advantage.

Message:  1012
<ce> Your expert level of stealth lets you evade the vampire's
<ce> tracking. He stops, trying to get his bearings on you.

QBN:
Clock _timer_ 1.00:00
Clock _failsafe_ 60.00:00

Foe _enemy_ is Ancient_vampire

--	Quest start-up:
    start timer _failsafe_

_level_ task:
    level 6 completed

_thau_ task:
    when skill Thaumaturgy is at least 80

_stealth_ task:
    when skill Stealth is at least 95

_time_ task:
    daily from 0:01 to 3:00

_enter_ task:
    when pc enters graveyard
    clear _exit_

_exit_ task:
    when pc exits graveyard
    clear _enter_

_meet_ task:
    when _time_ and _enter_ and _level_
    say 1010
    create foe _enemy_ every 0 minutes 1 times with 100% success
    start timer _timer_
    start task _spawn_

variable _spawn_
_safe1_ task:
    when _spawn_ and _thau_
    say 1011
    restrain foe _enemy_

_safe2_ task:
    when _spawn_ and _stealth_
    say 1012
    restrain foe _enemy_

_slain_ task:
    killed _enemy_
    end quest

_timer_ task:
    end quest

_failsafe_ task:
    end quest