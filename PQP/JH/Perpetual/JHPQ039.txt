-- Perpetual Quest
Quest: JHPQ039
DisplayName: Crazy Taxi I
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHPQ039

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHPQ039

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHPQ039

QuestFail:  [1003]
<ce>          ERROR 1003 JHPQ039

QuestComplete:  [1004]
<ce> "Well, that was quite the trip, now wasn't it?
<ce> I do hope this reward is worth your while."
<ce> 
<ce> _esc_ gives you a small bag of
<ce> money and departs with a wave.
<--->
<ce> "Ah, we have arrived! Thank you, %ra."
<ce> 
<ce> _esc_ gives you a small bag of
<ce> money and departs with a smile.
<--->
<ce> "You certainly know your way around %crn,
<ce> don't you %pcf? Thank you for helping me."
<ce> 
<ce> _esc_ gives you a small bag of
<ce> money and departs.

Message:  1010
<ce> _esc_ looks at you hopefully.
<ce> 
<ce> "Do you think you can take me to ___vill_?"

Message:  1011
<ce> "Thank you. I will follow. Lead the way."

Message:  1020
<ce> You are stopped by someone dressed in finery,
<ce> clearly in a bind.
<ce> 
<ce> "You there! Excuse me %ra, my name
<ce> is _esc_ and I need to get to a
<ce> rest area called ___vill_
<ce> within =2vill_ days. Are you headed toward it?"
<ce> 
<ce> You take a moment to check your map
<ce> and journal before answering.
<--->
<ce> Someone in fine clothing calls out to you.
<ce> 
<ce> "Excuse me! My name is _esc_ and I
<ce> need to get to a place called ___vill_.
<ce> My usual companions have failed me and I have
<ce> to be there urgently. Will you accept =reward_ gold
<ce> to take me there? I only have =2vill_ days."
<ce> 
<ce> Before answering, you take a moment
<ce> to check your map and journal.

Message:  1025
<ce> _esc_ interrupts you.
<ce> 
<ce> "%ra, did we not agree to get to
<ce> ___vill_ within =2vill_
<ce> days? I am quite disappointed, and I hope not
<ce> to associate myself with your company again."
<ce> 
<ce> Without further hesitation %g1 leaves.

Message:  1030
%qdt:
 _esc_ has asked for my help
 in getting to ___vill_
 within =2vill_ days.

Message:  1080
<ce> "Then my money will go to someone else!"
<--->
<ce> "What a shame. Perhaps this is not worth the risk."
<--->
<ce> "%oth. Forget about it, then."

QBN:
Clock _2vill_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 15.00:00
Clock _conv_ 00:02

Item _reward_ gold

Person _esc_ group Local_3.0

Place _vill_ remote tavernhome

--	Quest start-up:
    start timer _failsafe_

_enter_ task:
    when pc enters city
    stop timer _failsafe_
	say 1020
    pc at _vill_ do _arrive_
    start timer _conv_
    log 1030 step 0

_exit_ task:
    when pc exits city
    clear _enter_

_fail_ task:
    when _exit_ and not _begin_
    end quest

variable _conv_
_conv1_ task:
    when _conv_ and _enter_
    prompt 1010 yes _begin_ no _quit_

_arrive_ task:
    give pc _reward_
    end quest

_begin_ task:
    say 1011
    add _esc_ face
    start timer _2vill_

_2vill_ task:
    drop _esc_ face
    say 1025
    end quest

_quit_ task:
  say 1080
  end quest

_failsafe_ task:
    end quest