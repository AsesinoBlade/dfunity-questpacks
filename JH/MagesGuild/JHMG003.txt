Quest: JHMG003
DisplayName: The Dark Artist
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          %pct, %crn grants us great
<ce>          autonomy concerning our methods of
<ce>          research, our purposes, our disciplinary
<ce>          pursuits, and so forth, provided we promote
<ce>          the "common good" with our scholarly work.
<ce>          A fascinating term, that. In exchange, we
<ce>          have something of an unspoken agreement,
<ce>          if you will, that when students of the magical
<ce>          arts become a menace to society, we are
<ce>          the first to remove the threat. Please help
<ce>          us to fulfill that agreement, %pcf.

RefuseQuest:  [1001]
<ce>            I assure you, %pct, had you
<ce>            permitted me to explain the gravity of
<ce>            this matter, you would not have refused.

AcceptQuest:  [1002]
<ce>             A worthy response. This duty falls upon
<ce>             every member of the guild in its due time.
<ce>             We will send you to ___res_.
<ce>             A guild member in the city, highly
<ce>             trustworthy, informs us that the
<ce>             errant magician, _necr_, hides
<ce>             and practices in _res_.
<ce>             I certainly hope necromancy inspires
<ce>             no fear in you, for that is what you
<ce>             have agreed to quash this day.
<ce>             Make this rogue magician one with
<ce>             the death that so fascinates %g2.
<ce>             We must know it is done in =timer_ days.
<ce>             Courage, good friend.

QuestFail:  [1003]
<ce>           The Mages Guild has waited for your
<ce>           announcement of _necr_'s death,
<ce>           and the time has now passed.

QuestComplete:  [1004]
<ce>            Splendidly done, %pct. This work
<ce>           ensures the general public lends the
<ce>           proper confidence to the Mages Guild.
<ce>           Would a _diam_ be a proper reward
<ce>           for this task, in your estimation?
<ce>           I pray you put it to good use.

Message:  1020
<ce>           How pathetic. Why will you fools
<ce>           continue to resist the power and
<ce>           beauty of the art of necromancy?
<ce>           Do you not see the hypocrisy in
<ce>           slaying me? But if you so insist,
<ce>         we will have our duel. Have at you.

Message:  1021
<ce>           "So be it! See the power that you scorn!"
<ce>           
<ce>           _necr_ mutters an
<ce>           incantation. You hear something stirring.

Message:  1022
<ce>             The necromancer falls to the ground
<ce>             without a word. Your duty is fulfilled.

Message:  1030
%qdt:
 I must dispatch a necromancer
 hiding in the city of ___res_
 for the Mages Guild, who will
 be in _res_.
 I have =timer_ days to report
 in ___questgiver_.

Message:  1099
<ce>        I recognize that the occult inspires
<ce>        a little fear in everyone, %pct.
<ce>        But I cannot help you slay
<ce>        =enemy_. You have =timer_ days.

QBN:

Person _questgiver_ group Questor male
Person _necr_ group Spellcaster

Item _diam_ diamond

Place _res_ remote house1

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is Mage
Foe _und1_ is Ghost
Foe _und2_ is Wraith
Foe _und3_ is Wraith

--   Quest start-up:
   start timer _timer_
   log 1030 step 0
   place npc _necr_ at _res_   

_conv_ task:
   clicked npc _necr_
   say 1020
   hide npc _necr_
   place foe _enemy_ at _res_

_inj_ task:
   injured _enemy_
   say 1021
   create foe _und1_ every 1 minutes 1 times with 70% success
   create foe _und2_ every 1 minutes 1 times with 70% success
   create foe _und3_ every 1 minutes 1 times with 70% success

_slain_ task:
   killed _enemy_
   say 1022

_victory_ task:
  when _qgclicked_ and _slain_
  give pc _diam_
  end quest

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    say 1099
    clear _qgclicked_ _clearclick_
 
_timer_ task:
    end quest