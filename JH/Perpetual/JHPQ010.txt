-- Perpetual Quest
Quest: JHPQ010
DisplayName: Werewolves, Werewolves Everywhere
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          ERROR 1000 JHPQ010

RefuseQuest:  [1001]
<ce>          ERROR 1001 JHPQ010

AcceptQuest:  [1002]
<ce>           ERROR 1002 JHPQ010

QuestFail:  [1003]
<ce>          ERROR 1003 JHPQ010

QuestComplete:  [1004]
<ce>       This wolf seemed so different from the
<ce>       rest. Only time will tell if it was the cause
<ce>       of the infection outbreak.

Message:  1010
<ce>       You hear the telltale snarl of a werewolf.

Message:  1011
<ce>      Another roaming werewolf. Is this going
<ce>      to become customary in %crn?

Message:  1012
<ce>      A page bearing a crest of a battle axe and
<ce>      a katana hurries to give you a letter
<ce>      before excusing herself to other tasks.
<--->
<ce>      A page bearing a crest of a battle axe and
<ce>      a katana hurries to give you a letter
<ce>      before excusing himself to other tasks.

Message:  1013
 %pcf,
 
 My name is _hunter_. You have no doubt noticed
 the dramatic increase in werewolf activity in
 ____place_ over the past weeks. Please
 make of me an ally, that we may work together and
 free the land of this epidemic. I will wait for you here.
 
                      _hunter_
                      _place_
                      ___place_, ____place_

Message:  1014
<ce>      A pleasure to meet you, %pcf. Too many
<ce>      try to dismiss this surge in werewolf activity.
<ce>      Some say it's just a hysteria of rumors
<ce>      unsubstantiated by reality. In truth, I just
<ce>      think they're too cowardly to fight. I'm
<ce>      pleased to have found one %ra who will not
<ce>      buckle under %pg3 phobias.
<ce>      
<ce>      I've already traveled to __dummyc1_ and
<ce>      __dummyc2_ to check on a few sightings. From
<ce>      these there is little to report. I pray some
<ce>      breakthrough come through soon, for this will
<ce>      get worse until someone slays the source, I am
<ce>      certain. Perhaps you may want to go looking
<ce>      about in some of the dank holes in the region.
<ce>      Even if it were just by luck, you may find the
<ce>      cause of this surge of lycanthropes. Best of luck,
<ce>      and visit a temple if you begin to feel feverish.

Message:  1015
<ce>        A harried page quickly puts a note into your
<ce>        hands and stumbles away wearily.

Message:  1016
  %pcf,
 
  I've narrowed it down. There was a series of attacks
  with alarming frequency near __dung_.
  Please hurry there. Whatever is causing this uptick
  in werewolf activity must be hiding inside. I need
  to travel to warn the citizenry to keep away from
  it. Please descend and do that work I cannot.
 
                      _hunter_

Message:  1030
%qdt:
 I met with _hunter_, a
 werewolf hunter who is traveling
 across ____place_ looking
 for the source of the surge in
 werewolf activity. We'll stay in
 contact in case either of us finds out
 something to help break the case.

Message:  1031
%qdt:
 _hunter_ has determined that
 the outbreak patterns show increased
 attacks near __dung_.
 While %g1 goes to inform the authorities,
 %g1 asked me to find and slay the vector
 to stop the spread of this plague.

Message:  1040
<ce>       This is no ordinary werewolf. In its
<ce>       eyes gleams a peculiar intelligence.

Message:  1041
 %pcf,
 
 The werewolves are sharply declining in number.
 I am certain your task was successful, however
 it may be been brought to pass. You and I
 have saved ____place_ from its werewolf
 epidemic. I owe you a great deal for your work.
 Know that you will be all I talk about at our
 campfire reunions this year. I have already
 informed the regional authority concerning your deed.
 
 I've also told them concerning __dung_.
 The origin may be gone, but other wolves
 will linger about the area. Travelers will need
 to avoid it for now. You may want to as well.
 In any case, I've left for __dummyc1_ and
 some of its surrounding locales. I hear of
 some repentant wolfman or wolfwoman in the
 vicinity who wants to return to normalcy. %god
 watch over you, %pcf. Farewell.
 
                      _hunter_

Message:  1079
<ce>   A page, wearing the familiar crest of an axe
<ce>   and katana, presents a scroll to you in silence
<ce>   and marches on to other tasks.

Message:  1080 
  %pcf,
 
  Our work is done, one could say. A rival team of werewolf
  hunters managed to find the origin in __dung_
  and slay it. I suppose no fanfare or medal will be forthcoming
  from ____place_, but we can feel content that
  the citizenry are safe for now. %god go with you.
 
                      _hunter_

Message:  1081 
<ce>   It seems there is nothing left to do,
<ce>   besides slaying any leftover werewolves.

Message:  1099
<ce>   I can offer you no more counsel. I'll do what I can to
<ce>   find the culprit. For the meanwhile, you get on out there
<ce>   and keep their population down.

QBN:
Person _hunter_ group Noble

Item _letter_ letter used 1013
Item _letter2_ letter used 1016
Item _letter3_ letter used 1041

Place _place_ remote tavern
Place _dung_ remote dungeon
Place _dummyc1_ remote hamlet
Place _dummyc2_ remote village

Clock _timer_ 120.00:00 130.00:00
Clock _timer2_ 4.00:00 9.00:00
Clock _timer3_ 3.00:00 5.00:00
Clock _reveal_ 9.00:00 12.00:00
Clock _inc_ 15.00:00 20.00:00
Clock _inc2_ 20.00:00 25.00:00
Clock _inc3_ 15.00:00 20.00:00
Clock _end_ 15.00:00

Foe _enemy_ is Werewolf
Foe _enemy2_ is Werewolf
Foe _enemy3_ is Werewolf
Foe _enemy4_ is Werewolf
Foe _enemy5_ is Werewolf
Foe _boss_ is Werewolf

--	Quest start-up:
	start timer _timer_
          say 1010
         pick one of _norm_ _norm_ _norm_ _dud_
         pc at _dung_ set _there_
         start task _wolves_
         place foe _boss_ at _dung_

variable _dud_
variable _norm_

_wolves_ task:
         create foe _enemy_ every 5000 minutes 2 times with 100% success

_slay1_ task:
          killed _enemy_
          start timer _timer2_
          start timer _inc_

variable _timer2_
_wolves2_ task:
          when _timer2_ and _wolves_
          create foe _enemy2_ every 600 minutes indefinitely with 25% success

_expo_ task:
         killed 2 _enemy_
         killed 1 _enemy2_
         say 1011
         start timer _timer3_

_timer3_ task:
         give pc _letter_ notify 1012
         place npc _hunter_ at _place_

_qgclicked_ task:
	clicked npc _hunter_

_talk_ task:
         when _qgclicked_ and not _dialog_
         log 1030 step 0
         say 1014
         clear _qgclicked_
         start task _dialog_
         start timer _reveal_

_talk2_ task:
         when _qgclicked_ and _dialog_
         say 1099
         clear _talk2_ _qgclicked_

variable _dialog_
variable _reveal_
_findhunt_ task:
        when _reveal_ and not _dud_
        give pc _letter2_ notify 1015
        log 1031 step 1
        reveal _dung_

_falseend_ task:
         when _reveal_ and _dud_
         say 1079
         say 1080
         say 1081
         end quest

variable _inc_
variable _inc2_
variable _inc3_
_wolves3_ task:
          when _inc_ and _wolves_
          create foe _enemy3_ every 444 minutes indefinitely with 35% success        
          start timer _inc2_

_wolves4_ task:
          when _inc2_ and _wolves_
          create foe _enemy4_ every 333 minutes indefinitely with 30% success        
          start timer _inc3_

_wolves5_ task:
          when _inc3_ and _wolves_
          create foe _enemy5_ every 222 minutes indefinitely with 25% success        

_there_ task:
         start task _arrival_

variable _arrival_
_arrival2_ task:
         when _arrival_ and _reveal_
         say 1039

_final1_ task:
        injured _boss_
        say 1040

_final2_ task:
        killed _boss_
        give pc nothing
        stop timer _timer_
        clear task _wolves_
        give pc _letter3_ notify 1079
        remove log step 0
        remove log step 1
        start timer _end_

_timer_ task:
         end quest

_end_ task:
      end quest