Quest: RLQimprovereputation
DisplayName: Random Little Quest Improve Legal Reputation
-- Message panels
QRC:

QuestComplete:  [1004]
<ce>          It's hard to tell whether this is legitimate,
<ce>    but the stranger at least knows your name and background.
<ce>    It's worth a shot.
<ce>    
<ce>    "Okay. I'm gonna start moving things.
<ce>    Get some sleep or whatever, and in a few hours
<ce>    I'll have some documents burned. See you around.
<ce>    By the way, banishment isn't included. I can't fix that."
<ce>    
<ce>    The figure departs with flowing ease.

Message:  1010
<ce>           You barely make out a figure wearing dark clothes,
<ce>    who seems to be waiting for you. He gestures for you to approach.
<ce>    
<ce>    "You're %pcf, right? Word has it %reg is sick of you.
<ce>    I've got some people in high places who can change that,
<ce>    but it costs money. Give me a thousand and I'll make things better."

Message:  1011
<ce>            You refuse, and the figure shuffles off.
<ce>    
<ce>    "Fine. Whatever."

Message:  1012
<ce>             You scrounge around your pockets but do not
<ce>           find 1000 gold. An eyeroll and a sigh are your
<ce>    only answer before the figure departs.

QBN:

Clock _timer_ 4:00 5:00

--	Quest start-up:

_startup_ task:
    player legal-repute is lower than -5
    prompt 1010 yes _pay_ no _reject_

_reject_ task:
    say 1011
    end quest

variable _pay_
_payhim_ task:
    when _pay_ and not _given_
    pay 1000 money do _given_ otherwise do _nomoney_

_given_ task:
    give pc nothing
    start timer _timer_

_timer_ task:
    legal repute +10
    end quest

_nomoney_ task:
    say 1012
    end quest

_nogo_ task:
    when not _startup_
    end quest