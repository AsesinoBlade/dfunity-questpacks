Quest: JHMG015
DisplayName: To Free the Shackled
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Actually, we have a matter of immediate import.
<ce> I do hope you'll accept this. It's a bit of a long
<ce> story, but the short version is that one of our
<ce> local members, _dummy_, has been
<ce> cursed by some band of shapeshifters to assume
<ce> the form of a beast. Mercenaries from the
<ce> Fighters Guild are now poised to kill %g2,
<ce> unaware that %g1 is innocent. Will you
<ce> defend our poor friend?

RefuseQuest:  [1001]
<ce> Then %g3 life will be on your conscience should
<ce> %g1 not survive the attempt!

AcceptQuest:  [1002]
<ce> Well, I gave you the short version, and now the long
<ce> one. The shapeshifting spell is advanced, to the point
<ce> that _dummy_ actually thinks %g1 is an animal.
<ce> I feel certain %g1 will attack you on sight. No matter
<ce> what else happens in _res_, you must not
<ce> slay %g2. That is an inviolable directive. Fight off
<ce> the mercenaries who arrive and report to me when you
<ce> are finished. I have here a book I think you
<ce> will relish as a reward. And did I forget to mention
<ce> you must get there immediately? The Fighters Guild
<ce> will already be on the move by now.

QuestFail:  [1003]
<ce> There is no doubt by now the Fighters Guild
<ce> will have slain _dummy_.

QuestComplete:  [1004]
<ce> Oh, what a hero! The spell will wear off by the
<ce> latter part of a day, or perhaps even now it has,
<ce> but that will be too soon for more mercenaries
<ce> to get word of it. _dummy_ will know
<ce> about your work and praise your name. You have
<ce> truly saved a life this day. Take this
<ce> magnificent book, =reward_,
<ce> as my heartfelt thanks for protecting the innocent.

RumorsDuringQuest:   [1005]
Them Shapeshifters are awful people, I says.
<--->
You heard about that fellow cursed by the Shapeshifters? Wonder if he deserved it.

Message:  1010
<ce> It appears you have arrived in the nick of
<ce> time. You can already hear multiple voices
<ce> engaged in raucous discussion outside.

Message:  1011
<ce> You hear no more shouting. Hopefully these are the last
<ce> mercenaries sent to slay _dummy_.

Message:  1020
<ce> _dummy_ has been slain by your hand,
<ce> and the Mages Guild will not forget it.

Message:  1030
%qdt:
 _qgiver_ has sent me
 to _res_ immediately
 to defend a local member named
 _dummy_ from a band of
 mercenaries. I should go as quickly
 as humanly possible.

QBN:
Item _reward_ book

Person _qgiver_ group Questor male
Person _dummy_ faction The_Mages_Guild
Person _fg_ faction The_Fighters_Guild

Place _res_ local random

Clock _timer_ 00:30
Clock _failsafe_ 60.00:00

Foe _dk1_ is Spider
Foe _dk2_ is Giant_scorpion
Foe _dk3_ is Grizzley_bear
Foe _dk4_ is Harpy
Foe _bk1_ is 2 Barbarian
Foe _bk2_ is 2 Knight

--	Quest start-up:
	start timer _failsafe_
	log 1030 step 0 
	pick one of _d1_ _d2_ _d3_ _d4_
    pick one of _b1_ _b2_
	pc at _res_ set _inside_

variable _d1_
variable _d2_
variable _d3_
variable _d4_
variable _b1_
variable _b2_

_inside_ task:
    say 1010
    stop timer _timer_

_notinside_ task:
    when not _inside_ and not _slain_
    start timer _timer_

_spawn1_ task:
    when _inside_ and _d1_
    create foe _dk1_ every 0 minutes 1 times with 100% success

_spawn2_ task:
    when _inside_ and _d2_
    create foe _dk2_ every 0 minutes 1 times with 100% success

_spawn3_ task:
    when _inside_ and _d3_
    create foe _dk3_ every 0 minutes 1 times with 100% success

_spawn4_ task:
    when _inside_ and _d4_
    create foe _dk4_ every 0 minutes 1 times with 100% success

_bk1_ task:
    when _inside_ and _b1_
    create foe _bk1_ every 1 minutes 1 times with 75% success

_bk2_ task:
    when _inside_ and _b2_
    create foe _bk2_ every 1 minutes 1 times with 75% success

_slain_ task:
	killed 2 _bk1_
	killed 2 _bk2_
	say 1011
	stop timer _failsafe_
    change repute with _fg_ by -15

_fail_ task:
    killed 1 _dk1_
    killed 2 _dk1_
    killed 3 _dk1_
    killed 4 _dk1_
    say 1020
    change repute with _dummy_ by -18
    end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_ 
	clear _qgclicked_ _clearclick_ 

_pcreward_ task:
	when _qgclicked_ and _slain_ 
	give pc _reward_
	end quest

_failsafe_ task:
	end quest

_timer_ task:
	say 1003
	end quest