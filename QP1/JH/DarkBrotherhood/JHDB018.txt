Quest: JHDB018
DisplayName: The Debt Collector
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  A mediocre creature living in %crn
<ce>  has acquired a debt exceeding six figures,
<ce>  obligating the creditors to turn to us.
<ce>  Death is now the fool's only future.
<ce>  You may carry it out at your leisure.

RefuseQuest:  [1001]
<ce>  Looking for courier work instead?

AcceptQuest:  [1002]
<ce>  Look for the owner of _res_, found in
<ce>  ___res_. Whoever it is will probably be armed
<ce>  since this isn't the first time someone's
<ce>  tried to settle this. Tell me the tale of
<ce>  this mark's dismemberment within =timer_ days.

QuestFail:  [1003]
<ce>  The Dark Brotherhood required your report on
<ce>  the delinquent homeowner in ___res_ by now.

QuestComplete:  [1004]
<ce>  Well done. As demonstrated, we fill an important role.
<ce>  Let no one swindle with impunity while we
<ce>  have agents to do our work.

Message:  1010
<ce>  This must be the well-armed debtor.

Message:  1030
%qdt:
 The Dark Brotherhood of ___qgiver_
 has sent me to ___res_ to settle
 a delinquent debt in _res_.
 I need to report my success in =timer_ days.

QBN:
Person _qgiver_ group Questor male

Place _res_ remote house2

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _m1_ is Nightblade
Foe _m2_ is Vampire
Foe _m3_ is Werewolf

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0
    pick one of _e1_ _e2_ _e3_

_e1_ task:
    place foe _m1_ at _res_

_e2_ task:
    place foe _m2_ at _res_

_e3_ task:
    place foe _m3_ at _res_

_ipr_ task:
  injured _m1_
  injured _m2_
  injured _m3_
  say 1010

_slain_ task:
	killed _m1_
    killed _m2_
    killed _m3_

_timer_ task:
	end quest

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_ 

_pcreward_ task:
	when _qgclicked_ and _slain_
	give pc nothing
	end quest 