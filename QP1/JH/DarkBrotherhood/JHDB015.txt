Quest: JHDB015
DisplayName: Catharsis
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  A small herd of so-called Brotherhood "hunters"
<ce>  has taken up to hiding in a remote cave in
<ce>  %crn. We need someone skilled to teleport
<ce>  in to their den and wipe them all out. Will you?

RefuseQuest:  [1001]
<ce>  Perhaps you're looking for one of those non-violent contracts.
<ce>  That is, the ones that don't exist.

AcceptQuest:  [1002]
<ce>  Excellent. Take this _gem_. Its enchantment
<ce>  will send you into the depths. The place is warded
<ce>  sufficiently to warn the group concerning any entrance
<ce>  or exit except by this means. Therefore, once you're
<ce>  in, don't think about leaving unless you desire to
<ce>  report your failure. The gem will lose its power
<ce>  in about 6 hours' time, which will allow you to
<ce>  prepare. Bring me word of your success once
<ce>  you are certain this vigilante band is no more.
<ce>  May the Night Mother bless your work, %pct.

QuestFail:  [1003]
<ce>  The gem's magical essence ebbs away.
<ce>  You now have no means to find the band of manhunters.

QuestComplete:  [1004]
<ce>  Well done. The %t will have to think twice before
<ce>  trying a ruse like this again. Fine work, %pcf.
<--->
<ce>  Comically enough, the %t laid this plan to
<ce>  replace us with something more malleable.
<ce>  Surely we will have more such bands to hunt in the future.

Message:  1010
<ce>  Once you activate it, the _gem_'s magical power
<ce>  swells up and begins churning powerfully.
<ce>  A blinding light obliges you to close your eyes.

Message:  1011
<ce>  The gem has shattered under the magical force.
<ce>  The band of Brotherhood hunters will appear
<ce>  somewhere in this pit.

Message:  1012
<ce>  Your lying in wait has paid off.
<ce>  The band has arrived, ready for a fight.

Message:  1013
<ce>  This appears to be all of them.
<ce>  Now to report to _qgiver_.

Message:  1014
<ce>  Leaving ___dung_ has tripped the wards placed
<ce>  by the hunting band. Any chance of finding them is now long gone.

Message:  1030
%qdt:
 The Dark Brotherhood of ___qgiver_
 has given me a _gem_ which will send
 me to a pit, refuge to a group of
 headhunters working against our kind.
 I am not to leave the cave until
 they have been slain.

QBN:
Person _qgiver_ group Questor male

Item _gem_ gem

Place _dung_ remote dungeon

Clock _gtimer_ 6:00 7:00
Clock _spawn_ 8:00 2.00:00

Foe _e1_ is Knight

--	Quest start-up:
	start timer _gtimer_ 
	log 1030 step 0 
    get item _gem_
    _gem_ used do _tele_
	pc at _dung_ set _inside_

_gtimer_ task:
    say 1003
    end quest

_tele_ task:
    stop timer _gtimer_
    say 1010
    teleport pc to _dung_
    say 1011
    take _gem_ from pc
    start timer _spawn_

variable _inside_
_fail_ task:
    when _tele_ and not _inside_ and not _slain_
    say 1014
    end quest

_spawn_ task:
    say 1012
    create foe _e1_ every 1 minutes 3 times with 80% success

_slain_ task:
	killed 3 _e1_
    say 1013

_pcgetsgold_ task:
	when _slain_ and _qgclicked_
	give pc nothing
	end quest 

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_ 
	clear _qgclicked_ _clearclick_ 