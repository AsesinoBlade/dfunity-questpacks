Quest: JHDB011
DisplayName: No Trespassing
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  The Brotherhood has grown tired of a 
<ce>  nosy member of the Thieves Guild who has
<ce>  taken a curiosity to our guild halls. This
<ce>  task will be simple, should you accept it.

RefuseQuest:  [1001]
<ce>  I see where your loyalties lie.

AcceptQuest:  [1002]
<ce>  The buffoon's hiding place is in ___res_.
<ce>  Look for _res_. Leave an example for
<ce>  the other members of the Thieves Guild. Take
<ce>  no more than =2res_ days. When the killing is
<ce>  done, report to me at your leisure.

QuestFail:  [1003]
<ce> If all Dark Brotherhood contracts took this
<ce> long to fulfill, no one would fear its name.

QuestComplete:  [1004]
<ce> Very good. No one is exempt from Mephala's wrath
<ce> when duly incurred. Your loyalty has been noted.

Message:  1021
<ce> What?! No! Someone help!
<--->
<ce> No! It's the Dark Brotherhood!

Message:  1031
%qdt:
_questgiver_ assigned me
to slay an intrusive member of
the Thieves Guild in ___res_,
hiding in _res_.
The accounting must be done within
=2res_ days. I can return to the
guild hall without haste afterward.

Message:  1041
<ce> The Thieves Guild will learn
<ce> its proper place in time. This
<ce> is another grain of sand to tip
<ce> the balance.

QBN:
Person _questgiver_ group Questor male
Person _dummytg_ faction The_Thieves_Guild

Place _res_ remote house1

Clock _2res_ 00:00 0 flag 1 range 0 2
Clock _failsafe_ 180.00:00

Foe _thief_ is Burglar

--	Quest start-up:
	start timer _2res_ 
  start timer _failsafe_
	place foe _thief_ at _res_ 
	log 1031 step 0 

_ith_ task:
  injured _thief_
  say 1021

_slain_ task:
	killed 1 _thief_ saying 1041
  change repute with _dummytg_ by -10
  stop timer _2res_

_qgclicked_ task:
	clicked npc _questgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_ 
	clear _qgclicked_ _clearclick_ 

_pcreward_ task:
	when _qgclicked_ and _slain_ 
  give pc nothing
	end quest 

_2res_ task:
	say 1003
  end quest

_failsafe_ task:
  end quest