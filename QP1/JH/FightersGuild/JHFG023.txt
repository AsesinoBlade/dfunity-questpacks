Quest: JHFG023
DisplayName: Into the Tiger's Den
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>    Got a cave out in %crn that needs some culling.
<ce>    A pack of tigers is getting too strong and
<ce>    starting to kill a lot of wildlife. Gold
<ce>    reward goes to the bloke who takes their
<ce>    numbers down a notch. Is 'at you?

RefuseQuest:  [1001]
<ce>    All right, lemme see what else I have.

AcceptQuest:  [1002]
<ce>     All right, place's called ___dung_.
<ce>   The farmers near it have pooled together
<ce>   =reward_ gold for the one who clears it out.
<ce>   If you're back in =timer_ days, I'll give it
<ce>   to you. Bring me proof that eight are dead.
<ce>   That's the number they asked for. They've got
<ce>  white tufts on their ears, so if you wanna bring
<ce>  those back that's probably proof enough.

QuestFail:  [1003]
<ce>   You got proof of eight dead tigers?
<ce>  I can't give you the gold 'til I see it. I've
<ce>   got to give it over to the farmers afterward.

QuestComplete:  [1004]
<ce>   Yep, that'll do it. Nice work.
<ce>  This earns us a good name. Take the gold.

Message:  1010
<ce>   This looks like one of the tigers
<ce>  _questgiver_ told you about.
<ce>  You take some proof that you've killed it.

Message:  1011
<ce>  Eight tigers with white tufts on their ears
<ce>  have fallen. _questgiver_ will
<ce>  have your money now.

Message:  1030
%qdt:
 __questgiver_ has told
 me about a pack of tigers living
 in ___dung_ that needs
 to be put down. I should kill at
 least eight of them and then
 return to ___questgiver_ for my
 reward within =timer_ days.

QBN:
Item _reward_ gold

Person _questgiver_ group Questor male

Place _dung_ remote dungeon5

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _tiger_ is Sabretooth_tiger

--	Quest start-up:
	start timer _timer_
	log 1030 step 0
	pc at _dung_ set _inside_
    reveal _dung_

_inside_ task:
	create foe _tiger_ every 4 minutes indefinitely with 5% success
	create foe _tiger_ every 261 minutes indefinitely with 100% success

_find_ task:
    killed 1 _tiger_
    say 1010

_kill_ task:
    killed 8 _tiger_
    say 1011

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _kill_
	say 1003
	clear _qgclicked_ _clearclick_

_pcreward_ task:
	when _qgclicked_ and _kill_
	give pc _reward_
	end quest

_timer_ task:
	end quest