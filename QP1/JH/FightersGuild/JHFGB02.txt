-- Perpetual Quest
Quest: JHFGB02
DisplayName: Get Out of Jail Free
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                             JHFGB02 1000

RefuseQuest:  [1001]
<ce>                             JHFGB02 1001

AcceptQuest:  [1002]
<ce>                             JHFGB02 1002

QuestFail:  [1003]
<ce>             Your deadline to pay _qg_'s debt has passed.

QuestComplete:  [1004]
<ce>              Good day. Are you here to make a payment?
<ce>
<ce>                 With regards to _qg_... yes, I see.
<ce>        Well then, I accept your payment of 1267 gold pieces.
<ce>               That will balance and close the account.
<ce>                    Thank you for visiting _bank_.

Message:  1010
<ce>                Head on over to _place_ here in town.
<ce>                _qg_'s gonna be waiting for you there.

Message:  1011
<ce>                       Hey there! Who sent you?
<ce>
<ce>               Oh good, the Fighters Guild never fails.
<ce>            Listen, this is the sort of problem I've got.
<ce>                I'm trying to get out of ____bank_ but
<ce>             I haven't been able to come up with the last
<ce>             installment on a loan I took out last year.
<ce>           It wasn't much, but the interest got kinda ugly.
<ce>             I need someone to pay off the remaining 1267
<ce>              gold so I can get out of here without any
<ce>          skullcrackers coming after me. Could you do that?
<ce>             I've got noble family here in ____bank_ and
<ce>           they could give your reputation a real makeover.

Message:  1012
<ce>        Phew, that's a weight off my back. You need to get the
<ce>                    money to _banker_ in ___bank_.
<ce>         Here's the catch: the payment's due in =2bank_ days.
<ce>     That's not a lot of time but it'd mean the world to me. Just
<ce>       a fair warning though, we're on a gentlemen's agreement,
<ce>       right? Don't try screwing with me here, 'cause if I find
<ce>         the bank's still sending people after me, I'm gonna
<ce>           tell my family all about it and you'll be sorry.
<ce>               Anyway, thanks a bunch. I'm outta here.

Message:  1013
<ce>           No hard feelings. Thanks for saying it straight.
<ce>       I'll head back to the Fighters Guild in a little bit and
<ce>              ask for someone else. Good travels to you.

Message:  1014
<ce>                     Yes, what is this regarding?
<ce>
<ce>                 In the matter of _qg_, there is some
<ce>           unfinished business but I cannot speak about it
<ce>        without prior authorization. Do you have a payment to
<ce>           make? We require that it be made in gold pieces.
<ce>         Letters of credit will not suffice on this occasion.

Message:  1030
 %qdt: 
I am to meet _qg_ at _place_
in ___place_ to hear the task I will undertake.

Message:  1031
 %qdt:
_qg_ has asked me to pay off a debt
owed to _bank_ in ___bank_,
which will require 1267 gold pieces.
In exchange I am told my reputation in
____bank_ will improve.
The assigned agent is named _banker_.

QBN:
Place _place_ local tavern
Place _bank_ remote bank

Clock _2bank_ 00:00 0 flag 2 range 0 2
Clock _failsafe_ 90.00:00

Person _qg_ faction The_Thieves_Guild
Person _banker_ group Librarian

--	Quest start-up:
    say 1010
    place npc _qg_ at _place_
    log 1030 step 0
    start timer _failsafe_

_begin_ task:
    clicked npc _qg_
    prompt 1011 yes _accept_ no _reject_

_accept_ task:
    start timer _2bank_
    say 1012
    place npc _banker_ at _bank_
    hide npc _qg_
    log 1031 step 1

_reject_ task:
    say 1013
    end quest

_pay_ task:
    clicked _banker_ and at least 1267 gold otherwise do _nopay_
    give pc nothing
    legal repute +10
    end quest

_nopay_ task:
    say 1014

_2bank_ task:
    say 1003
    legal repute -20
    end quest

_failsafe_ task:
    end quest