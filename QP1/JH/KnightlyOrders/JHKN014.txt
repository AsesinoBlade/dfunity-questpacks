Quest: JHKN014
DisplayName: Wartime Negotiations
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> We have here a missive on behalf of
<ce> the %t that is intended to resolve a rather
<ce> violent dispute between %crn and
<ce> a very dangerous group of criminals.
<ce> Will you escort the letter, in the
<ce> name of the %t?

RefuseQuest:  [1001]
<ce> Well, it would have required tact anyway.

AcceptQuest:  [1002]
<ce> You will take the message to ___res_,
<ce> where negotiations are being held in a
<ce> private location, _res_.
<ce> All you will need to do is present
<ce> the document and return. We hope it
<ce> will accelerate the end of their conflict.

QuestFail:  [1003]
<ce> Their suspicions get the best of them.
<ce> One side assumes you are reinforcements
<ce> for the other and draws weapons. The other
<ce> side reciprocates, and before you speak a single
<ce> word, both sides have begun to fight. This
<ce> is not what _qgiver_ intended.

QuestComplete:  [1004]
<ce> How did it go? Ah, a step in the right
<ce> direction. I certainly hope they
<ce> continue in that way. Well done, %pct.
<ce> Remember that to avert war is the work
<ce> of the gods. Save lives, don't take them.

Message: 1020
<ce> You enter amidst a fierce argument between
<ce> two very loud people. Their yelling seems
<ce> to test the strength of the nearby walls.
<ce> You are entirely ignored until the door shuts
<ce> behind you, at which point the building falls
<ce> silent. The air is heavy. They want something
<ce> from you.

Message: 1021
<ce> Seeing the letter, the crony tears it from your
<ce> hands and rips the seal open. After %g1 gives
<ce> it a cursory look, %g1 sends it to the arguing
<ce> people. You hear nothing. And then,
<ce> 
<ce> "We have yet to see how this grants restitution
<ce> for our fallen brethren. How can you not..."
<ce> 
<ce> The letter appears to have arrived exactly
<ce> on time to prevent a brawl and the loss
<ce> of life. They have begun to speak civilly
<ce> to one another. _qgiver_
<ce> will want a report on the matter.
<--->
<ce> Seeing the letter, the crony tears it from your
<ce> hands and rips the seal open. After %g1 gives
<ce> it a cursory look, %g1 sends it to the arguing
<ce> people. You hear nothing. And then,
<ce> 
<ce> "And this is all? The %t must consider
<ce> what we've lost over months of battle."
<ce> 
<ce> The letter appears to have arrived exactly
<ce> on time to prevent a brawl and the loss
<ce> of life. They have begun to speak civilly
<ce> to one another. _qgiver_
<ce> will want a report on the matter.

Message:  1030
%qdt:
 _qgiver_ has assigned me a missive
 from the %t to help with some sort of
 dispute resolution taking place in
 _res_ at ___res_.
 I must deliver the letter and
 return to ___qgiver_.

QBN:
Item _lett_ letter

Person _qgiver_ group Questor male

Place _res_ remote house1

Clock _delay_ 00:01
Clock _quick_ 00:01
Clock _failsafe_ 120.00:00

Foe _enemy1_ is 6 Nightblade
Foe _enemy2_ is 6 Knight

--	Quest start-up:
  pc at _res_ set _inside_
	log 1030 step 0
  get item _lett_
  start timer _failsafe_

_inside_ task:
  _lett_ used do _pers_
  play sound door_slam every 0 minutes 1 times
  say 1020
  start timer _quick_

_quick_ task:
  play sound sword_ready every 0 minutes 1 times
  say 1003
  take _lett_ from pc
  start timer _delay_

_spawn_ task:
  when _inside_ and _quick_
  create foe _enemy1_ every 0 minutes 1 times with 100% success
  create foe _enemy2_ every 0 minutes 1 times with 100% success

_delay_ task:
  end quest

_pers_ task:
	stop timer _quick_
  take _lett_ from pc
  play sound talking_medium every 0 minutes 1 times
  say 1021

_qgclicked_ task:
	clicked npc _qgiver_ 

_pcgetsgold_ task:
	when _qgclicked_ and _pers_ 
	give pc nothing
  legal repute 3
	end quest

_clearclick_ task:
	when _qgclicked_ and not _pers_ 
	clear _qgclicked_ _clearclick_ 

_failsafe_ task:
  end quest