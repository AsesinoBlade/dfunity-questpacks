Quest: JHKN019
DisplayName: The Champion
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> %pcf, I will not mince words. We have in our
<ce> possession an ancient and powerful artifact, of the kind that very
<ce> rarely sees the light of day. We have been authorized to take it
<ce> out of storage as incentive for the task I now reveal to you.
<ce> One of the Daedric princes, as is expected, grew furious with
<ce> a summoner for invoking him under false pretenses, and
<ce> now desires revenge. The Mages Guild representative managed
<ce> to bargain with the Daedra, saying that one of our champions
<ce> could withstand the hordes of oblivion as a sort of propitiation.
<ce> To make many words few, we need someone capable of slaying
<ce> an army of daedra. Please tell me it will be you.

RefuseQuest:  [1001]
<ce> Then in our darkest hour you have forsaken us.

AcceptQuest:  [1002]
<ce> Take this bell then. Go to the palace here in
<ce> ___qgiver_. Prepare yourself in every meaningful way.
<ce> Ring the bell only when you are certain you are
<ce> ready to face Oblivion itself. There are no words for
<ce> what you will be facing. The artifact we will give you will
<ce> seem paltry in comparison, in my opinion. Truly, you
<ce> will have earned the title Champion in our order for
<ce> years to come. I will warn you, however, that the
<ce> bell is an object created by the daedra, and
<ce> that it will only last one week. If it disappears
<ce> our bargain will as well. May every god over Tamriel
<ce> go with you into the palace.

QuestFail:  [1003]
<ce> The bell disappears from your belongings.

QuestComplete:  [1004]
<ce> _qgiver_ reverently reaches into a nearby
<ce> pack and gives you your reward. There is no other
<ce> congratulation, other than a respectful bow.

Message:  1010
<ce> This is the place to ring the bell
<ce> once you are certain you're prepared.

Message:  1011
<ce> You barely begin to ring the bell when
<ce> the entire building trembles. It feels
<ce> as though a current of air rushes through
<ce> two enormous windows at opposing sides
<ce> of the room. The daedric forces are more
<ce> than ready to prove their might against
<ce> the sole champion of Tamriel.

Message:  1020
<ce> Everything in the palace freezes in place, including you.
<ce> All things stop in a dread silence. Then you hear a voice speak
<ce> which appears to come from the very walls surrounding you.
<ce> 
<ce> "What trickery is this? Did you think to hide from my hordes?
<ce> How utterly pathetic. Away with you, filthy mortal."
<ce> 
<ce> You feel sick to your stomach. Before you can
<ce> so much as blink, your environment changes.

Message:  1021
<ce> (Note: You may be in a location that's impossible to escape from,)
<ce> (like inside an enclosed cage or at the bottom of a chute.)
<ce> (Make a save before trying the console command "teleport" to get out.)

Message:  1025
<ce> You flee outside, leaving the daedric army within. You hear
<ce> laughter erupt from beyond the door, and then various noises
<ce> resembling destruction of things. You have failed the test.

Message:  1029
<ce> In an instant, all the daedra freeze. You hear
<ce> a chuckle reverberate throughout the building,
<ce> not originating in any particular point. A
<ce> voice speaks, which you suppose to be the prince.
<ce> 
<ce> "Very amusing! We could certainly continue on in
<ce> this vein for quite some time, couldn't we %ra?
<ce> But I consider our bargain fulfilled. You have proven
<ce> yourself one with power among Daedra."
<ce> 
<ce> The voice ceases. As a parting gift, the surrounding
<ce> daedra remain. Now that your part is over, the knights
<ce> can arrive soon to put down the remaining forces.
<ce> You are free to leave the building.

Message:  1030
%qdt:
 The daedric hordes of a daedric prince
 must be appeased, and combat by trial is
 the only way. I have been given one week
 to prepare for the battle. When I am ready,
 I should ring the bell I have been given
 inside the palace of ___qgiver_.
 _qgiver_ has promised
 me an artifact in exchange for this duty.

QBN:
Clock _failsafe_ 7.00:00
Clock _trick_ 00:10
Clock _battle_ 00:01

Person _qgiver_ group Questor male

Foe _dlord_ is Daedra_lord
Foe _dsed_ is Daedra_seducer
Foe _daedroth_ is Daedroth

Place _pal_ local palace
Place _faildung_ remote dungeon

Item _art1_ artifact Auriels_Bow
Item _art2_ artifact Auriels_Shield
Item _art3_ artifact Chrysamere
Item _art4_ artifact Lords_Mail
Item _art5_ artifact Necromancers_Amulet
Item _art6_ artifact Staff_of_Magnus
Item _art7_ artifact Warlocks_Ring
Item _bell_ bell

--	Quest start-up:
  start timer _failsafe_
  log 1030 step 0
  pick one of _a1_ _a2_ _a3_ _a4_ _a5_ _a6_ _a7_
  pc at _pal_ set _inside_
  get item _bell_

variable _a1_
variable _a2_
variable _a3_
variable _a4_
variable _a5_
variable _a6_
variable _a7_

_inside_ task:
  _bell_ used do _invoke_
  say 1010

_invoke_ task:
  take _bell_ from pc
  play sound belltower every 0 minutes 1 times
  stop timer _failsafe_
  start timer _trick_
  start timer _battle_
  say 1011

_war_ task:
  when _invoke_ and not _victory_
  play sound whisting_wind_4 every 1 minutes 30 times
  create foe _dlord_ every 0 minutes 1 times with 100% success
  create foe _dsed_ every 0 minutes 1 times with 75% success
  create foe _daedroth_ every 0 minutes 1 times with 100% success
  create foe _daedroth_ every 0 minutes 1 times with 100% success
  create foe _dlord_ every 1 minutes indefinitely with 85% success
  create foe _dsed_ every 1 minutes indefinitely with 75% success
  create foe _daedroth_ every 1 minutes indefinitely with 95% success

_minustrick_ task:
  killed 2 _dlord_
  killed 2 _dsed_
  killed 3 _daedroth_

variable _trick_
_epicfail_ task:
  when _trick_ and not _minustrick_
  say 1020
  change repute with _qgiver_ by -22
  make pc ill with Plague
  teleport pc to _faildung_
  say 1021
  end quest

_bigfail_ task:
  when _invoke_ and not _inside_ and not _trick_ and not _victory_
  say 1025
  change repute with _qgiver_ by -22
  end quest

_slain_ task:
  killed 8 _dlord_
  killed 10 _dsed_
  killed 12 _daedroth_

variable _battle_
_victory_ task:
  when _slain_ and _battle_
  say 1029

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _victory_
	clear _qgclicked_ _clearclick_ 

_pcgetsgold_ task:
	when _qgclicked_ and _victory_
  legal repute 30

_v1_ task:
  when _a1_ and _pcgetsgold_
  give pc _art1_
  end quest

_v2_ task:
  when _a2_ and _pcgetsgold_
  give pc _art2_
  end quest

_v3_ task:
  when _a3_ and _pcgetsgold_
  give pc _art3_
  end quest

_v4_ task:
  when _a4_ and _pcgetsgold_
  give pc _art4_
  end quest

_v5_ task:
  when _a5_ and _pcgetsgold_
  give pc _art5_
  end quest

_v6_ task:
  when _a6_ and _pcgetsgold_
  give pc _art6_
  end quest

_v7_ task:
  when _a7_ and _pcgetsgold_
  give pc _art7_
  end quest

_failsafe_ task:
  say 1003
	end quest 