Quest: JHKNA01
DisplayName: Well-Intentioned Extremists
-- Message panels
QRC:

QuestFail:   [1003]
<ce>  The band of vigilantes no doubt has had plenty
<ce>  of notice concerning your intentions. They will
<ce>  have scattered into the countryside by now.

QuestComplete:  [1004]
<ce>  Your work here is finished. With its leadership
<ce>  dead, this vigilante band will have no more
<ce>  power over ___tr_.

RumorsDuringQuest:   [1005]
Buncha miserable folks in town.
<--->
The %t's kinda useless, wouldn't you say?
<--->
We don't need the %t. We're fine on our own.
<--->
We're all doomed.

RumorsPostSuccess:   [1007]
___tr_ had a bunch of criminal lowlifes taking over.
<--->
Ah, the hero of ___tr_! How are you?
<--->
I hear the %t sent an army to wipe out ___tr_.

Message:  1010
<ce>  You search this cretin's pockets for any clues,
<ce>  but it appears this group covers its tracks well.
<ce>  Once you get up, a woman approaches and speaks softly.
<ce>  
<ce>  "Excuse me? Yes, are you going to stop the band?
<ce>  I can't speak here. Please find me in my home,
<ce>  _res_. Come quickly."
<--->
<ce>  You search this cretin's pockets for any clues,
<ce>  but it appears this group covers its tracks well.
<ce>  Once you get up, a woman approaches and speaks softly.
<ce>  
<ce>  "Thank you for helping. Are you here to stop them?
<ce>  Please, come see me in _res_.
<ce>  I can't talk here. Hurry."

Message:  1011
<ce>  You are already too late. This body is that of
<ce>  the woman who approached you outside, without
<ce>  even the chance to tell you her name.
<ce>  
<ce>  A creaking sound nearby indicates that
<ce>  her murderers lay in wait for you.

Message:  1012
<ce>  With both dead you begin ruffling through
<ce>  their belongings for any clues, but find
<ce>  nothing. Then a voice shouts at the door.
<ce>  
<ce>  "Hey, we burying the broad and the %ra together?"
<ce>  
<ce>  Hearing no answer, the brigand enters the home.

Message:  1013
<ce>  Your strike makes this miscreant freeze in terror.
<ce>  Clearly not all of them are combat-ready.
<ce>  You demand answers a way to find the band's leader.
<ce>  
<ce>  "Please, let me live! _tr_!
<ce>  Go there! Don't do this to me, I swear they're there!"

Message:  1014
<ce>  There are no hostiles here. Was the brigand lying?
<ce>  Has the band's leader stepped out? Or is there something
<ce>  more to find inside?

Message:  1015
<ce>  A group of assassins was somehow hiding within these walls.
<ce>  They waste no time attempting to butcher you.

Message:  1016
<ce>  Apparently the fracas was loud enough that some
<ce>  peasants gathered at the door, wondering at your fate.
<ce>  One man summons up the courage to speak to you.
<ce>  
<ce>  "Are you really strong enough to beat them? Please,
<ce>  go to _goal_! I know they'll be there soon!
<ce>  We don't want them here! Help us!"
<ce>  
<ce>  He begs you with his hands clasped together, and several
<ce>  others agree with the sentiment. Perhaps this may
<ce>  actually be the way to the end.
<--->
<ce>  Apparently the fracas was loud enough that some
<ce>  peasants gathered at the door, wondering at your fate.
<ce>  One woman summons up the courage to speak to you.
<ce>  
<ce>  "Are you really strong enough to beat them? Please,
<ce>  go to _goal_! I know they'll be there soon!
<ce>  We don't want them here! Help us!"
<ce>  
<ce>  She begs you with her hands clasped together, and several
<ce>  others agree with the sentiment. Perhaps this may
<ce>  actually be the way to the end.

Message:  1017
<ce>  You try to open the door silently but it apparently had
<ce>  a magical ward placed on it. You presently hear a shout.
<ce>  
<ce>  "Who's there?! No interruptions!"
<ce>  
<ce>  You conjure no meaningful answer. The air grows heavy.
<ce>  Soon the band's leaders leap up from their place
<ce>  and rush to slay you.

Message:  1018
%qdt:
 A woman at ___res_'s entrance has asked me
 to meet her at _res_ to
 give me information.

Message:  1019
%qdt:
 One of the criminal band in ___res_ told
 me to go to _tr_ to find the leaders.

Message:  1020
%qdt:
 Some well-wishing peasants told me to find
 the group's leaders in _goal_.

Message:  1021
<ce>  %oth, my poor heart. I was just terrified that
<ce>  it might not have been you. Forgive me.
<ce>  The entire city is under watch right now.
<ce>  This band has eyes everywhere. The town guard
<ce>  refuses to intervene, saying that each
<ce>  skirmish is just "criminals fighting it out."
<ce>  Filthy, shameless accomplices, that's all they are.
<ce>  
<ce>  So I'd like to make a suggestion. Since the town
<ce>  is well scouted, and your sudden appearance is
<ce>  no doubt raising some eyebrows, why not take
<ce>  advantage of it? Head outside and just loiter around
<ce>  for a little while. You'll be targeted by some of
<ce>  the criminal scum, and once you've slain a few,
<ce>  maybe that'll get their attention enough to send
<ce>  some of their more important people at you.
<ce>  I mean, their numbers are finite after all, and if
<ce>  they start to run out, they'll need to get rid of
<ce>  you one way or another. Why not give it a try?
<ce>  But please, if you run into trouble, don't ask me
<ce>  for help. I'm no warrior. I'm just trying to help.

Message:  1022
<ce>  Surprisingly enough (to you, at least), the plan worked.
<ce>  A band of well-armed enforcers has come to claim your life.

Message:  1023
<ce>  You find a small note among your foes' belongings,
<ce>  which you pick up and add to your pack.

Message:  1024
<ce>  Tonight's fallback: _goal_.
<ce>  Cast the entry spell first,
<ce>  or the alarm's gonna sound.

Message:  1025
<ce>  Whatever enchantment you needed to know, you didn't.
<ce>  A loud whistling sound shoots from the door's hinges.
<ce>  Any chance of launching a surprise attack is now gone.

Message:  1026
<ce>  You find a small _gem_ among your foes' belongings.
<ce>  It is inscribed, "In case of emergency."

Message:  1027
<ce>  The gem has warped you directly into the
<ce>  band's secret hideout, which was not in
<ce>  ___res_ at all. Its mastermind
<ce>  and a pet are understandably shocked to see
<ce>  you appear instead of a trusted colleague.
<ce>  
<ce>  (Hint: You may be in a location that's impossible to escape from,)
<ce>  (like the bottom of a chute. If so,)
<ce>  (make a save and try the console command "teleport" to get out.)

Message:  1028
%qdt:
 A well-intending peasant in ___res_
 has suggested that I step outside and slay
 a few of the vigilantes to attract attention.
 Hopefully someone important will appear
 once I make myself stubbornly visible.

Message:  1030
%qdt:
 ___res_ has been overtaken
 by a group of self-proclaimed enforcers
 of the law. I am to vanquish the band
 of vigilantes, taking no more than one
 day once I am inside.

QBN:

Person _lady_ group Banker female

Place _res_ local random
Place _dung_ remote dungeon
Place _goal_ local random
Place _tr_ local random

Foe _e1_ is Rogue
Foe _e2_ is Assassin
Foe _e3_ is Barbarian
Foe _e4_ is Nightblade
Foe _e5_ is Giant_scorpion
Foe _e6_ is Rogue
Foe _e7_ is Archer
Foe _e8_ is Warrior
Foe _e9_ is Spellsword
Foe _e10_ is Nightblade
Foe _e11_ is Mage
Foe _e12_ is Bard
Foe _e13_ is Rogue

Item _body_ dead_body
Item _gem_ gem
Item _letter_ letter used 1024

Clock _timer_ 1.00:00
Clock _delay_ 0:02 0:03
Clock _delay2_ 0:10

--	Quest start-up:
    start timer _timer_
    log 1030 step 0
    create foe _e6_ every 0 minutes 1 times with 100% success
    start task _spawn_

_spawn_ task:
    create foe _e1_ every 120 minutes indefinitely with 50% success

_begin_ task:
    killed _e6_
    say 1010
    pick one of _f1_ _f2_
    log 1018 step 1

_f1_ task:
    place npc _lady_ at _res_

_conv_ task:
    clicked npc _lady_
    say 1021
    clear _spawn_
    start task _spawn2_
    log 1028 step 2

_spawn2_ task:
    create foe _e7_ every 60 minutes indefinitely with 50% success

_step_ task:
    killed 5 _e7_
    clear _spawn2_
    hide npc _lady_
    start timer _delay2_

_delay2_ task:
    say 1022
    create foe _e3_ every 0 minutes 3 times with 100% success

_step2_ task:
    killed 3 _e3_
    pick one of _k1_ _k2_

_k1_ task:
    say 1023
    get item _letter_
    pc at _goal_ do _final_

_final_ task:
    say 1025
    create foe _e8_ every 0 minutes 1 times with 100% success
    create foe _e9_ every 0 minutes 1 times with 100% success
    create foe _e10_ every 0 minutes 1 times with 100% success
    create foe _e11_ every 0 minutes 1 times with 100% success

_et8_ task:
    killed _e8_

_et9_ task:
    killed _e9_

_et10_ task:
    killed _e10_

_et11_ task:
    killed _e11_

_victory1_ task:
    when _et8_ and _et9_ and _et10_ and _et11_
    give pc nothing
    end quest

_k2_ task:
    say 1026
    get item _gem_
    _gem_ used do _tele_

_tele_ task:
    teleport pc to _dung_
    say 1027
    create foe _e4_ every 0 minutes 1 times with 100% success
    create foe _e5_ every 0 minutes 1 times with 100% success

_victory2_ task:
    killed _e4_
    give pc nothing
    end quest

_f2_ task:
    place item _body_ at _res_

_bt_ task:
    clicked item _body_
    say 1011
    create foe _e2_ every 0 minutes 2 times with 100% success
    take _body_ from pc

_bt2_ task:
    killed 2 _e2_
    say 1012
    create foe _e12_ every 0 minutes 1 times with 100% success
    restrain foe _e12_

_inj_ task:
    injured _e12_
    say 1013
    restrain foe _e12_
    log 1019 step 3
    pc at _tr_ do _entry_

_entry_ task:
    say 1014
    start timer _delay_
    clear _notentry_

_notentry_ task:
    when not _entry_
    stop timer _delay_

_delay_ task:
    say 1015
    create foe _e13_ every 0 minutes 3 times with 100% success

_slain2_ task:
    killed 3 _e13_
    pc at _goal_ do _ambush_

_peas_ task:
    when _slain2_ and not _entry_
    say 1016
    log 1020 step 4

_ambush_ task:
    say 1017
    create foe _e8_ every 0 minutes 1 times with 100% success
    create foe _e9_ every 0 minutes 1 times with 100% success
    create foe _e10_ every 0 minutes 1 times with 100% success
    create foe _e11_ every 0 minutes 1 times with 100% success

_timer_ task:
    say 1003
    end quest