-- Innkeeper and Merchant
Quest: JHIMU004
DisplayName: A Game at Dinner
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>  You bored? Some friends and I are
<ce>  gonna start up a game of blackjack
<ce>  pretty soon. Entry fee is 50,
<ce>  and you'll get 300 if you win.
<ce>  Want in, %ra?

RefuseQuest:  [1001]
<ce>  You'd probably lose anyway.

AcceptQuest:  [1002]
<ce>   Then come back in two hours.
<ce>  We'll be here then.

QuestComplete:  [1004]
<ce>  In spite of some murmuring nearby,
<ce>  _qgiver_ hands you the prize pool.

Message:  1010
<ce>  "Good, you're here. We're about to get started."
<ce>  
<ce> _qgiver_ takes your fifty gold pieces,
<ce>  and some of %g3 friends mill in to take
<ce>  their places nearby. One of them speaks.
<ce>  
<ce>  "Welcome %ra, good to see a new face.
<ce>  You know the rules to blackjack, right?
<ce>  You want 21 points, and you add each new card
<ce>  to your total. If you go over 21, you lose.
<ce>  Closest or fastest to 21 wins.
<ce>  It's that simple. We play for keeps here."

--path1
Message:  1011
<ce>  Your first card is a 6.
<ce>  
<ce>  Total: 6. Keep going?

--path1a
Message:  1012
<ce>  Your second card is a 5.
<ce>  
<ce>  Total: 11. Keep going?

Message:  1013
<ce>  Your third card is a 4.
<ce>  
<ce>  Total: 15. Keep going?

Message:  1014
<ce>  Your fourth card is a 6.
<ce>  
<ce>  Total: 21. Keep going?

--path1b
Message:  1015
<ce>  Your second card is a queen.
<ce>  
<ce>  Total: 16. Keep going?

Message:  1016
<ce>  Your third card is a 4.
<ce>  
<ce>  Total: 20. Keep going?

Message:  1017
<ce>  Your fourth card is a 9.
<ce>  
<ce>  Total: 29.
<ce>
<ce>  "Ha, you lose! Later sucker!"

Message:  1018
<ce> You decide to stay at 20.
<ce> One by one, the other players charge
<ce> ahead and surpass 21. In the end,
<ce> you are the only player to remain.

--path2
Message:  1019
<ce>  You draw a king.
<ce>  
<ce>  Total: 10. Keep going?

--path2b
Message:  1020
<ce>  You draw a 2.
<ce>  
<ce>  Total: 12. Keep going?

Message:  1021
<ce>  You draw a 3.
<ce>  
<ce>  Total: 15. Keep going?

Message:  1022
<ce>  You draw a 2.
<ce>  
<ce>  Total: 17. Keep going?

Message:  1023
<ce>  You draw an ace.
<ce>  
<ce>  Total: 18. Keep going?

Message:  1024
<ce> As you pick up your next card,
<ce> one of the other competitors
<ce> shouts loudly.
<ce> 
<ce> "I got 21, you thugs! Pay up!"
<ce> 
<ce> It looks like you can't win them all.

--path2b
Message:  1025
<ce>  You draw a 7.
<ce>  
<ce>  Total: 17. Keep going?

Message:  1026
<ce>  You draw a 4.
<ce>  
<ce>  Total: 21. Keep going?

Message:  1030
<ce>  You show your total of 21.
<ce>  The other players look at
<ce>  your cards closely, and one
<ce>  looks at a cut edge to make
<ce>  sure it's from the local deck.
<ce>  Once they're satisfied, they concede.

Message:  1031
<ce> They didn't count on someone being as lucky as you.
<ce> You show a 21, inciting various degrees of disgust
<ce> and fury from your fellow competitors.

Message:  1032
<ce> "No, somethin's not right here. This %ra's
<ce> tryinna cheat us out of our beer money!
<ce> I'm not havin' it! %oth!"
<ce> 
<ce> One of your former competitors grabs a weapon and
<ce> attempts to get the money from you by force.

Message:  1033
<ce> This last card puts you past 21.
<ce> You slump down as the game continues on
<ce> without you. It doesn't take long for another
<ce> player to win, taking the prize money.

Message:  1034
<ce> Your conservative play earns you nothing.
<ce> Two other players manage to get higher
<ce> scores than you, and one of them takes
<ce> the prize money proudly.

Message:  1050
%qdt:
 I've reserved a place to
 play blackjack with a
 =qgiver_ and some of %g3
 friends in __qgiver_.
 The entry fee is 50 gold.
 They'll be there in two hours.

Message:  1051
<ce> I don't see your 50 gold pieces. Where are they?

QBN:
Item _reward_ gold range 300 to 300

Person _qgiver_ group Questor

Clock _failsafe_ 4:00
Clock _wait_ 2:00

Foe _nme_ is Bard

--	Quest start-up:
	start timer _failsafe_
	log 1050 step 0
    start timer _wait_
    hide npc _qgiver_

_wait_ task:
    restore _qgiver_

_click_ task:
    clicked _qgiver_ and at least 50 gold otherwise do _nomoney_
    say 1010
    remove log step 0
    pick one of _deal1_ _deal2_

_nomoney_ task:
    say 1051

_deal1_ task:
    prompt 1011 yes _deal1x_ no _insuff_

_deal1x_ task:
    pick one of _deal1a_ _deal1b_

_deal1a_ task:
    prompt 1012 yes _deal1aa_ no _insuff_

_deal1aa_ task:
    prompt 1013 yes _deal1ab_ no _insuff_

_deal1ab_ task:
    prompt 1014 yes _over_ no _victory_

_deal1b_ task:
    prompt 1015 yes _deal1ba_ no _insuff_

_deal1ba_ task:
    prompt 1016 yes _over1_ no _conservwin_

_over1_ task:
    say 1017
    end quest

_deal2_ task:
    prompt 1019 yes _deal2xa_ no _insuff_

variable _deal2xa_
_lucky_ task:
    when attribute Luck is at least 90

_deal2xb_ task:
    when _lucky_ and _deal2xa_
    say 1031
    give pc _reward_
    start task _anger_

_anger_ task:
    say 1032
    create foe _nme_ every 0 minutes 1 times with 100% success

_deal2x_ task:
    when _deal2xa_ and not _lucky_
    pick one of _deal2a_ _deal2b_

_deal2a_ task:
    prompt 1020 yes _deal2az_ no _insuff_

_deal2az_ task:
    prompt 1021 yes _deal2aa_ no _insuff_

_deal2aa_ task:
    prompt 1022 yes _deal2ab_ no _insuff_

_deal2ab_ task:
    prompt 1023 yes _deal2ac_ no _insuff_

_deal2ac_ task:
    say 1024
    end quest

_deal2b_ task:
    prompt 1025 yes _deal2ba_ no _insuff_

_deal2ba_ task:
    prompt 1026 yes _over_ no _victory_

_victory_ task:
    say 1030
	give pc _reward_ 

_conservwin_ task:
    say 1018
    give pc _reward_

_failsafe_ task:
	end quest

_insuff_ task:
    say 1034
    end quest

_over_ task:
    say 1033
    end quest