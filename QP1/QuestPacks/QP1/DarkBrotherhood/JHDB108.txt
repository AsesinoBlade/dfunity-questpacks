Quest: JHDB108
DisplayName: Into Thin Air
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                 Experts of the culling craft such as
<ce>             ourselves occasionally must take particular
<ce>            measures to ensure anonymity, lest we find our
<ce>       guild hall turned cinders at the start of the new week.
<ce>            We have one such matter that cannot be trusted
<ce>            to a novice. You have been given the honor of
<ce>             carrying out this sensitive task. Accept it.
                                     <--->
<ce>           Every member of the Dark Brotherhood should feel
<ce>            a deep sense of pride in the work we perform.
<ce>          All who belong must work continually to become the
<ce>       best assassins in all Tamriel. To that end, we must now
<ce>           carry out a somewhat complex accounting, and you
<ce>               have been chosen to take it to fruition.

RefuseQuest:  [1001]
<ce>                   What do you hope to accomplish?
                                     <--->
<ce>                   Ah, true, I need someone who is
<ce>                 not a coward. I'll continue waiting.

AcceptQuest:  [1002]
<ce>                 Go to ___dungeon_. We have arranged
<ce>               for our dear noble to be transported to
<ce>            its depths in =2dungeon_ days. A Wizard's Rend
<ce>          spell will accompany the teleportation to prevent
<ce>          a Recall spell, but it will wear off with ordinary
<ce>            rest. Take no more than seven hours to finish
<ce>               the job. We have =reward_ gold pieces to
<ce>           give to you once the matter is finished. Now go.
<ce>                For our sake, you cannot fail in this.

QuestFail:  [1003]
<ce>            You have run out of time. The Dark Brotherhood
<ce>              will be held responsible for your failure.

QuestComplete:  [1004]
<ce>         Well now, did you enjoy yourself, %pcf? I certainly
<ce>                      hope so. Take your reward.

Message:  1020
<ce>                You have committed the perfect crime.
<ce>          It'll be years before the body is even identified.
<ce>                    Now to return to _questgiver_.

Message:  1021
<ce>            The Dark Brotherhood's mark will have arrived
<ce>                     in ___dungeon_ by now. There
<ce>                         is no time to lose.

Message:  1022
<ce>             Knowing the end is at hand, your mark enters
<ce>           into the hysterics of end-of-life negotiations.
<ce>
<ce>          "Please! Please! Stop, I know the %t! If you will
<ce>          only let me live, I will exalt your name in %crn!
<ce>          I can help you reverse whatever damaged reputation
<ce>          you may have here! Please, just grant me my life!"
                                     <--->
<ce>             Knowing the end is at hand, your mark enters
<ce>           into the hysterics of end-of-life negotiations.
<ce>
<ce>          "No! No, I beg you, stop! Listen, I can help you!
<ce>          A hired murderer must have a tarnished reputation
<ce>          in %crn, and I can help you repair it! Just let me
<ce>          live and I swear on the life of the %t that I will
<ce>            give you influence you have not known before!"

Message:  1023
<ce>          You appear to have reached an agreement. Your new
<ce>           friend thanks you for your kindness and vows to
<ce>           work ceaselessly to give you a new name in %crn.
<ce>           You presume that the promise will be fulfilled,
<ce>           as it will not be hard to repeat this scenario.

Message:  1030
%qdt:
 _questgiver_ of the
 Dark Brotherhood, ___questgiver_,
 has sent me to ___dungeon_
 to await the arrival of a noble
 in =2dungeon_ days. I must
 finish the job before a Recall
 spell can be cast. I will be
 paid =reward_ gold pieces.

QBN:
Item _reward_ gold range 1201 to 1399

Person _questgiver_ group Questor male

Place _dungeon_ remote dungeon9

Clock _2dungeon_ 00:00 0 flag 1 range 0 2
Clock _killtimer_ 07:15 09:00
Clock _return_ 180.00:00

Foe _enemy1_ is Ranger
Foe _enemy2_ is Bard
Foe _enemy3_ is Knight

--	Quest start-up:
	start timer _2dungeon_
	log 1030 step 0
	pick one of _E.01_ _E.02_ _E.03_
	reveal _dungeon_
	pick one of _m1_ _m2_

variable _E.01_
variable _E.02_
variable _E.03_
variable _m1_
variable _m2_

_2dungeon_ task:
	say 1021
	start timer _killtimer_

_spawn1_ task:
	when _E.01_ and _2dungeon_
	place foe _enemy1_ at _dungeon_

_spawn2_ task:
	when _E.02_ and _2dungeon_
	place foe _enemy2_ at _dungeon_

_spawn3_ task:
	when _E.03_ and _2dungeon_
	place foe _enemy3_ at _dungeon_

_inj_ task:
	injured _enemy1_
	injured _enemy2_
	injured _enemy3_
	change foe _enemy1_ infighting true
	change foe _enemy2_ infighting true
	change foe _enemy3_ infighting true

_prom_ task:
	when _inj_ and _m2_
	prompt 1022 yes _mercy_ no _normal_

_mercy_ task:
	say 1023
	remove foe _enemy1_
	remove foe _enemy2_
	remove foe _enemy3_

variable _normal_
_slain_ task:
	killed 1 _enemy1_ saying 1020
	killed 1 _enemy2_ saying 1020
	killed 1 _enemy3_ saying 1020
	stop timer _killtimer_
	start timer _return_

_qgclicked_ task:
	clicked npc _questgiver_

_clearclick_ task:
	when _qgclicked_ and not _slain_
	clear _qgclicked_ _clearclick_

_pcgetsgold_ task:
	when _qgclicked_ and _slain_
	give pc _reward_
	end quest

_gainrep_ task:
	when _killtimer_ and _mercy_ and not _slain_
	legal repute +20

_killtimer_ task:
	say 1003
	change repute with _questgiver_ by -13
	end quest

_return_ task:
	end quest