Quest: JHDB021a
DisplayName: To Kill an Ambassador
-- Message panels
--Needs AnyInfo or Rumors fix
QRC:

QuestComplete:  [1004]
<ce>  Greetings. Ah, you are the %ra I must thank.
<ce>  I am elated to know that the Dark Brotherhood
<ce>  will even pay heed to orcs. Their price was great,
<ce>  but much worth it.
<ce>  
<ce>  My people wish to live in peace, and our foes are
<ce>  many. You have done us much service. Had it been
<ce>  otherwise, the man Septim could have been persuaded
<ce>  to slay us all. Such venom from a spiteful counselor.
<ce>  
<ce>  Our gift to you comes at great cost, but
<ce>  with no regret. Never let it be said that
<ce>  the orcs will not recognize their benefactors.
<ce>  If you see the Emperor, tell him that. We
<ce>  will be faithful to our pacts.

Message:  1010
<ce>  "Well met. Please, let us step outside. We will
<ce>  be heard within these walls."
<ce>  
<ce>  She deftly tosses a bag of textiles she was
<ce>  carrying into a nearby corner and follows you.

Message:  1011
<ce>  The magician breathes in the fresh air and starts
<ce>  speaking, free of the dank halls of the palace.
<ce>  
<ce>  "Ugh, what a relief. I'm sure you're used to filthy
<ce>  opulence like that, but I am not. This air suits me
<ce>  so much better.
<ce>  
<ce>  "My name is Phythia Springhollow. My last several weeks
<ce>  have been engaged in trying to find out more about
<ce>  this apparent ambassador, but I can't so much as get
<ce>  a glimpse of him. Too important to go out in public.
<ce>  He's using messengers to communicate, but they're
<ce>  too good, even with alcohol. Wayrest's servants
<ce>  are no slouches, that's for sure.
<ce>  
<ce>  "My only lead is a fellow I bribed into opening up
<ce>   a bit. I suggest we hurry. No telling what could
<ce>  happen to loose-lipped informants in Wayrest.
<ce>  We need to go to _res_ here in town.
<ce>  Lead the way, %ra."

Message:  1012
<ce>  Phythia speaks.
<ce>  
<ce>  "We're a team, but we have different roles.
<ce>  I'm not nearly so capable in combat as you are.
<ce>  That's your part. I'm fluent in Orcish and quite
<ce>  good at persuasion. That's my part. If you
<ce>  keep me safe and let me talk to the important
<ce>  people, we can make this happen."

Message:  1013
<ce>  "It's so quiet," Phythia whispers. "Good to see
<ce>  him take some precaution, but this is eerie."

Message:  1014
<ce>  The front door bursts open, and several armed
<ce>  warriors march in.
<ce>  
<ce>  "Traitors to the Emperor! We know there's
<ce>  a plot afoot! Die like the dogs you are!"
<ce>  
<ce>  Phythia flees fluidly toward another section of
<ce>  of the house to avoid the bedlam. It's your turn.

Message:  1015
<ce>  With this last foe down, Phythia looks around
<ce>  the corner and makes hand signals to you.
<ce>  Convinced that all is safe, she returns to
<ce>  a normal stance and surveys the bodies.
<ce>  They hold no evidence of their source.
<ce>
<ce>  Then you hear a rustling sound within the house.
<ce>  
<ce>  "Hello? Ufina, is that you?"
<ce>  
<ce>  Phythia winks at you and answers, "Yes, it's me.
<ce>  The bad men are dead. We should talk quickly."
<ce>  
<ce>  The man responds, "Yes, we should. Hurry."

Message:  1016
<ce>  "What took you so long, Ufina? Fellows who know
<ce>  too much can't survive in this political atmosphere.
<ce>  I need to head to Menevia before they get me."
<ce>  
<ce>  Phythia caresses his forehead and answers smoothly,
<ce>  "I needed a bodyguard, you see. And this %ra
<ce>  is doing a superb job. Leave town, but tell me
<ce>  what I need to know about this wicked ambassador
<ce>  who enslaves all those poor children in Bruma.
<ce>  We need to confront this scoundrel for his crimes!"
<ce>
<ce>  The man appears almost hypnotized by her gaze.
<ce>  He regains composure and answers, "O-oh yeah.
<ce>  Well, his name is Agrane Uthane. He uses a lot
<ce>  of fake names when he's out doing business, but that
<ce>  one's the real one. Man's been hiding out because
<ce>  of a few attempts on his life these last few
<ce>  months. I say he deserves it 'cause of those
<ce>  poor children."

Message:  1017
<ce>  "Lots of rumors going on about that guy.
<ce>  Some people have started an investigation of
<ce>  their own, trying to see if there's something
<ce>  about him they can use to get him sent back
<ce>  to the Emperor. Talk with the townspeople here and
<ce>  use the code word '_tri_.' If they're friends
<ce>  of mine and they've found anything out about
<ce>  this jackal, they'll tell you."
<ce>  
<ce>  Phythia thanks him and gives him an inordinately
<ce>  passionate kiss, and he flees the residence. She
<ce>  then turns to you.
<ce>  
<ce>  "Sorry, that's just what this work requires sometimes.
<ce>  Didn't mean for you to see that. Well, lead the way.
<ce>  It's your show for now."

Message:  1018
_tri_? Shh. Go look in _restwo_ at ___restwo_.
<--->
You're looking for some _tri_? Oh, I wish I were that rich!
<--->
Your _tri_? It's lost? Hope you find it.
<--->
I can't help you go looking for your lost thing.
<--->
If I had a gold coin for every _tri_ I lost over the years! Hah!

Message:  1019
<ce> Phythia snickers. "I can't tell whether those
<ce> rumors about Agrane Uthane are true. Regardless,
<ce> the people believe them enough to lead us straight
<ce> where we need to go. I've had harder tasks."

Message:  1020
<ce> You step into _restwo_ with Phythia,
<ce> and she presses her finger to her lips.
<ce> Peering across the way, she tosses a dagger
<ce> through the air. It lands in an invisible
<ce> sentry, who falls with a groan.
<ce> 
<ce> "We said combat's your job, but you weren't
<ce> going to see that one. His stuff's mine,
<ce> by the way." She winks. "I never said
<ce> I couldn't kill, did I?"

Message:  1021
<ce> This paper merely contains directions to
<ce> a place called ___dung_ in
<ce> ____dung_. A small note explains that
<ce> the ambassador's possessions used to be kept
<ce> here, but were moved for fear of interlopers.

Message:  1022
<ce> Phythia shivers. "I'll wait outside, thank
<ce> you very much. I have plenty of other tasks
<ce> I can work on in the open air. I'll set a
<ce> link between us, so even if you Recall out
<ce> of here I'll be able to follow you. Come
<ce> see me outside if you find anything of worth."

Message:  1023
<ce> In this bag you find ordinary effects for an
<ce> ambassador such as ink used to place a
<ce> fingerprint seal, a few blank imperial letterheads
<ce> customized for one "_alias_,"
<ce> and a compass. However, you also find a few
<ce> vials of fresh blood and some ashes.

Message:  1024
<ce> "Well, did you find anything?" Phythia's eyes
<ce> light up as you let her explore the contents
<ce> of the ambassador's bag. "Oh, by %god!
<ce> This is amazing! The pig's a vampire!
<ce> Look at these vials, precisely what you'd
<ce> need to keep cover for so long! We're
<ce> about to put him on a spit, we are!
<ce> 
<ce> "It's a simple matter now. We just need to
<ce> find him, and I'll cast a spell to reveal
<ce> his nature. We'll have him roasting in a
<ce> single day!
<ce> 
<ce> "The name on that letterhead must be his
<ce> current alias. It says he's in ___palace_!
<ce>  Good going! Let's get moving!"

Message:  1025
I dunno, but some _alias_ from the Empire is staying over in ___palace_.
<--->
_alias_? That's a pretty common name. Not sure if I know who that is.
<--->
Why? Is this some kind of game? I dunno who that is.
<--->
Oh, you're looking for _alias_. Important figure but I dunno where.

Message:  1026
<ce>  "Good day. How may I help you, citizens?"
<ce> 
<ce> Phythia smirks and raises one hand dramatically.
<ce> "Behold, people of Wayrest! See the true nature
<ce> of this beast you call an ambassador! Suffer
<ce> the rays of the sun!"
<ce> 
<ce> She conjures up a ball of fire within her hand
<ce> which exudes a piercing shine. You have to cover
<ce> your eyes to maintain sight. Agrane Uthane shrieks
<ce> from the effects of her sunlight spell, and with
<ce> a fierce strike sends her to the floor.
<ce> 
<ce> "Imbeciles! See the suffering meted to those who
<ce> fight against His Majesty the Emperor!"
<ce> 
<ce> Quicker than you can react, he casts a spell on
<ce> several guards who have arrived to investigate.
<ce> Their eyes glaze over, and they point their weapons
<ce> at you rather than him. Worse still, you recognize
<ce> the chanting of a Recall spell. If you're going to
<ce> carry out your mission, you'd better do it quickly.

Message:  1027
<ce> Agrane Uthane cackles as he disappears into thin air.
<ce> Phythia is nowhere to be seen. The assassination
<ce> attempt has failed.

Message:  1028
<ce> The vampire gurgles, falling to the ground in
<ce> a limp state. His spell on the guards breaks,
<ce> and they approach to investigate his death.
<ce> Phythia's spell left his face deeply seared.
<ce> Combined with the evidence you show from
<ce> the pilfered bag, there is no question
<ce> concerning the ambassador's true nature.
<ce> 
<ce> The magician slowly rises up, first to her knees
<ce> and then on one and another foot. "Sorry.
<ce> I shouldn't have done that from so close.
<ce> That's my vanity coming to bite me again."
<ce> She coughs a little bit of blood but doesn't
<ce> bear any serious wounds.
<ce> 
<ce> "They said to talk to Gortwog for our reward.
<ce> I need to recover first, so I'll head there
<ce> when I'm better. You go on and see what he
<ce> has in store for you. Hope it's something good."
<ce> Phythia smiles weakly, shaken by the experience.
<ce> She then regains her composure and gives you
<ce> a firm handshake as a farewell before
<ce> heading to a nearby tavern.

Message:  1030
%qdt:
 I am on a mission with a magician
 of the Dark Brotherhood to stop
 the Emperor's ambassador to Wayrest.
 My partner has directed me to
 _res_ of Wayrest to
 speak to an informant.

Message:  1031
%qdt:
 The informant has told us that
 we might get information concerning
 the ambassador by asking the
 local townspeople about "_tri_."
 The man has a very hateful following
 and many who wish to see him fail.

Message:  1032
%qdt:
 Paperwork in _restwo_
 of ___restwo_ says that
 the ambassador's belongings are
 kept in ___dung_ of
 ____dung_.

-Message:  1033
-%qdt:
- My partner surmises that the
- ambassador is a vampire using the
- alias "_alias_." If we can
- ask around and find him, perhaps we
- can confront him and end this.

Message:  1033
%qdt:
 My partner surmises that the
 ambassador is a vampire using the
 alias "_alias_,"
 staying in ___palace_. Perhaps we
 can confront him and end this.

Message:  1034
%qdt:
 Together we slew the vampire and
 fulfilled our mission. My partner
 told me to speak to Gortwog in
 Orsinium for my reward.

Message:  1035
<ce>  Phythia suddenly stops. "Wait, I'm
<ce>  getting a message from afar."
<ce>  She struggles to understand, and
<ce>  then nods. "We've taken too long.
<ce>  The ambassador has returned to
<ce>  the Imperial City. We can't do this."
<ce>  Without much of a goodbye, she
<ce>  departs, following some instruction
<ce>  that you were excluded from hearing.

QBN:
Person _elf_ named Br'itsa
Person _alias_ faction Random_Noble male
-anyInfo 1025
Person _gort_ named Gortwog atHome
Person _inform_ faction The_Thieves_Guild male
Person _db_ faction The_Dark_Brotherhood

Place _palace_ remote palace
Place _dung_ remote dungeon
Place _res_ local house
Place _restwo_ remote house
Place _wayrest_ permanent WayrestCity
Place _orsin_ permanent OrsiniumCity

Clock _timer_ 00:00 0 flag 17 range 0 2
Clock _firsttalk_ 0:02 0:08
Clock _ffdelay_ 0:02
Clock _secondtalk_ 1:00 2:00
Clock _recall_ 0:03

Foe _warr_ is Warrior
Foe _guar_ is Knight
Foe _vamp_ is Ancient_vampire

Item _reward_ artifact Volendrung
Item _sack_ small_sack
Item _tri_ trinket anyInfo 1018
Item _paper_ letter

--	Quest start-up:
    say 1010
    pc at _wayrest_ do _begin_
    pc at _res_ do _firstfight_

_begin_ task:
    say 1011
    start timer _timer_
    log 1030 step 0
    start timer _firsttalk_
    start task _firstpic_

_firstpic_ task:
    add _elf_ face

_firsttalk_ task:
    say 1012

_firstfight_ task:
    say 1013
    start timer _ffdelay_

_nores_ task:
    when not _firstfight_
    stop timer _ffdelay_

_ffdelay_ task:
    create foe _warr_ every 0 minutes 4 times with 100% success
    say 1014
    drop _elf_ face

_ffdone_ task:
    killed 4 _warr_
    say 1015
    add _elf_ face
    place npc _inform_ at _res_

_fftalk_ task:
    clicked npc _inform_
    say 1016
    say 1017
    add dialog for item _tri_
    hide npc _inform_
    place item _paper_ at _restwo_
    pc at _restwo_ set _secres_
    start timer _secondtalk_
    log 1031 step 1

_secondtalk_ task:
    say 1019

_secres_ task:
    say 1020

_grabpaper_ task:
    clicked item _paper_
    say 1021
    reveal _dung_
    take _paper_ from pc
    place item _sack_ at _dung_
    pc at _dung_ do _dungintro_
    log 1032 step 1

_dungintro_ task:
    say 1022
    drop _elf_ face

_findsack_ task:
    clicked item _sack_
    say 1023
-    add dialog for person _alias_

_thirdtalk_ task:
    when _findsack_ and not _dungintro_
    add _elf_ face
    say 1024
    place npc _alias_ at _palace_
    log 1033 step 1

_confront_ task:
    clicked npc _alias_
    say 1026
    drop _elf_ face
    hide npc _alias_
    place foe _vamp_ at _palace_
    create foe _guar_ every 0 minutes 4 times with 100% success
    start timer _recall_
    change foe _vamp_ infighting true
    change foe _vamp_ team 19

_infight_ task:
    daily from 0:00 to 23:59
    change foe _guar_ infighting true
    clear _infight_

_recall_ task:
    remove foe _vamp_
    say 1027
    legal repute -70
    change repute with _db_ by -30
    end quest

_slain_ task:
    killed _vamp_
    stop timer _recall_
    remove foe _guar_
    say 1028
    reveal _orsin_
    log 1034 step 1

_qgclicked_ task:
    clicked npc _gort_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    clear _qgclicked_ _clearclick_

_pcreward_ task:
    when _qgclicked_ and _slain_
    give pc _reward_
    end quest

_timer_ task:
    say 1035
    drop _elf_ face
    end quest