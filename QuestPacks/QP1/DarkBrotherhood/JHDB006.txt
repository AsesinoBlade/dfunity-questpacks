Quest: JHDB006
DisplayName: Fundraising
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> Our primary craft and means of sustenance
<ce> is the resolution of loose ends, where merited.
<ce> We occasionally carry out other means of
<ce> fundraising when the opportunity arises.
<ce> We are informed of an unguarded child
<ce> belonging to people of little importance
<ce> beyond the size of their bank account.
<ce> We wish to have a part of said account.
<ce> Does this line of work interest you?
<--->
<ce> Sometimes the Brotherhood is given an
<ce> erroneous reputation as headhunters
<ce> and private executioners. That is one
<ce> aspect of our work, certainly, but we
<ce> do not limit ourselves to mere murder.
<ce> I have a request to transport a child from
<ce> one town to another in a quiet manner.
<ce> Will you make yourself responsible for it?

RefuseQuest:  [1001]
<ce> Fear not, %pct. We have plenty
<ce> of opportunities to shed blood,
<ce> where clearly your heart belongs.

AcceptQuest:  [1002]
<ce> Very well. The child's name is _kid_.
<ce> You will find %g2 in ___res_, within
<ce> _res_. Dispatch any guards
<ce> within at your leisure. We expect them
<ce> to present no great resistance. We
<ce> have designated _goal_ in
<ce> ___goal_ as a proper place to
<ce> deposit the child until the time of the ransom.
<ce> A member of the Brotherhood will await
<ce> you there. Mephala watch over you.

QuestFail:  [1003]
<ce> You have taken too much time. The Brotherhood's
<ce> ransom plan will likely fail at this point.

QuestComplete:  [1004]
<ce> You are stopped as you enter the building.
<ce> 
<ce> "Well done, %pct. We thank you on behalf
<ce> of the Night Mother for this service you
<ce> render us. Come now, _kid_.
<ce> We have a room set apart for you.
<ce> You'll just be dying to see it."
<ce> 
<ce> The child is led away from you,
<ce> and you are left alone.

Message:  1020
<ce>  This must be _res_.
<ce> _kid_ should be inside.

Message:  1021
<ce> Before _kid_ can say so much
<ce> as a word, you silence %g2, cover %g2 in
<ce> a robe, and pull %g2 close to you.

Message:  1030
%qdt:
 The Dark Brotherhood needs
 me to pick up a child from
 _res_, which is in
 ___res_. I must deliver
 the child to _goal_ in
 ___goal_ before
 =timer_ days are up.

Message:  1099
<ce> First to ___res_, then to
<ce> ___goal_. A simple task.

QBN:
Person _questgiver_ group Questor male
Person _kid_ faction Children

Place _res_ remote house
Place _goal_ remote house

Clock _timer_ 30.00:00

Foe _enemy_ is 2 Archer

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0 
	place npc _kid_ at _res_
  pc at _goal_ set _igol_
  pc at _res_ set _ires_

_ires_ task:
 say 1020
 create foe _enemy_ every 1 minutes 1 times with 75% success

_kclicked_ task:
	clicked npc _kid_
  say 1021
  hide npc _kid_

variable _igol_

_victory_ task:
	when _kclicked_ and _igol_
	give pc nothing
	end quest 

_qgclicked_ task:
	clicked npc _questgiver_ 
  say 1099
	clear _qgclicked_

_timer_ task:
  say 1003
	end quest