Quest: JHIM022
DisplayName: A Borrower or a Lender Be
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                 My name is _qg_. I find myself in a
<ce>           rather tight financial situation, and this month
<ce>          I do not know how close I will come to bankruptcy.
<ce>           A small investment of 1,000 gold pieces will be
<ce>        well-rewarded, I swear it. Would you have such a sum?

RefuseQuest:  [1001]
<ce>                  %oth. Someone will come, I'm sure.

AcceptQuest:  [1002]
<ce>               Good, very good. Talk to me in a moment
<ce>                       and I'll take the money.

QuestFail:  [1003]

QuestComplete:  [1004]
<ce>               A page calls your name and delivers you
<ce>            a sack of coins. On the side you find written,
<ce>
<ce>           "%pcn, thank you for keeping my business afloat.
<ce>          We made it! Let this be proof to you that we keep
<ce>                      our word here in ___qg_."
<ce>
<ce>      You empty the bag and find yourself =reward_ gold richer.

Message:  1020
<ce>                   %pcn, my deepest thanks to you.
<ce>                Give me time, and I swear before %god
<ce>               that you will not regret this decision.
<ce>              You will be paid back, and more! Farewell.

Message:  1021
<ce>               I see you don't have 1,000 gold pieces.
<ce>                             What a pity.

QBN:

Person _qg_ group Questor male

Item _reward_ gold range 2001 to 3499
Item _dummy_ large_sack

Clock _repay_ 30.00:00 80.00:00
Clock _dead_ 1.00:00

--   Quest start-up:
    start timer _dead_

_start_ task:
    clicked _qg_ and at least 1000 gold otherwise do _fail_
    say 1020
    start timer _repay_
    stop timer _dead_

_repay_ task:
    give pc _dummy_ notify 1004
    have _dummy_ set _endquest_

_endquest_ task:
    get item _reward_
    take _dummy_ from pc
    end quest

_fail_ task:
    say 1021

_dead_ task:
    end quest