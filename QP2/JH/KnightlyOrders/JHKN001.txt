Quest: JHKN001
DisplayName: Dragonling Nest
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>              My name is _qgiver_. This morning
<ce>             we received a courier on behalf of the %t
<ce>              requesting aid in ___dungeon_.
<ce>               Apparently there's a nest of dragonlings
<ce>                somewhere inside of a rare breed
<ce>                  worth protecting. Poachers have
<ce>                already begun entering, and it's
<ce>                  only a matter of time before
<ce>               there are no dragonlings left at all.
<ce>                Will you offer your hand in this?

RefuseQuest:  [1001]
<ce>                       A poor answer.

AcceptQuest:  [1002]
<ce>                   The %t has made abundantly
<ce>                   clear that this activity is illegal.
<ce>                  You are authorized to put to death
<ce>                 any who attempt it further. Hurry to
<ce>                   ___dungeon_ and make clear the
<ce>                    intent of our good ruler. I expect
<ce>                 this to be resolved in =timer_ days.
<ce>                         Go now, %pcf.

QuestFail:  [1003]
<ce>               And what will I tell the next
<ce>                courier to arrive, %pcf? To
<ce>              save their dragonlings themselves?

QuestComplete:  [1004]
<ce>                 Excellent work. Let no one
<ce>               disobey the %t with impunity.

Message:  1020
<ce>               The message has been made
<ce>                abundantly clear. These corpses
<ce>             will serve as a warning to any
<ce>                  others who trespass here.

Message: 1021
<ce>               You get the sinking feeling
<ce>               that this is not what you
<ce>                  were supposed to do.

Message:  1030
%qdt:
 _qgiver_ in
 ___qgiver_ has requested
 my aid in protecting a nest of
 dragonlings somewhere in
 __dungeon_. I must slay the
 invading poachers but none
 of the nesting dragons.
 I have =timer_ days.

Message:  1099
<ce>          Those dragonlings will not
<ce>           save themselves. Slay the
<ce>          criminals and return here.
<ce>          ___dungeon_, =timer_ days.

QBN:

Person _qgiver_ group Questor male

Place _dungeon_ remote dungeon

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _drag_ is Dragonling
Foe _enemy1_ is Knight
Foe _enemy2_ is Warrior

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0 
	place foe _drag_ at _dungeon_
	pc at _dungeon_ set _inside_
          reveal _dungeon_

variable _inside_
_quest_ task:
	when _inside_ and not _slain_
	create foe _enemy1_ every 15 minutes indefinitely with 26% success
	create foe _enemy2_ every 20 minutes indefinitely with 20% success

_timer_ task:
	end quest

_slain_ task:
	killed 3 _enemy1_ saying 1020 
	killed 3 _enemy2_ saying 1020 

_fail_ task:
	killed 1 _drag_ saying 1021
	end quest

_pcgetsgold_ task:
	when _qgclicked_ and _slain_ 
	give pc nothing
	end quest 

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _slain_ 
          say 1099
	clear _qgclicked_ _clearclick_ 