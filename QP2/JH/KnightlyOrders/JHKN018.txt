Quest: JHKN018
DisplayName: A Rash of Thefts
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ___qgiver_ is having some trouble with
<ce> some persistent thief that no one else can catch.
<ce> Would you be willing to search among the
<ce> townspeople to find the responsible party?

RefuseQuest:  [1001]
<ce> Worried you can't hold a conversation?

AcceptQuest:  [1002]
<ce> I'll give you two days. Report your success to me
<ce> by then. I should mention the odd fact that none
<ce> of the stolen items carries any real worth. One was
<ce> a _flam_ stolen from _hum1_.
<ce> Another was a very worn piece of clothing
<ce> taken from _hum2_. An imperceptible
<ce> thief should be spending his or her time searching for
<ce> bigger prizes than that. Perhaps you can put a stop
<ce> to the crime spree before it reaches that level.
<ce> I will await your report with itching ears.

QuestFail:  [1003]
<ce> _qgiver_ asked you to
<ce> find ___qgiver_'s thief by now.

QuestComplete:  [1004]
<ce> Oh, what a tale. A child was the culprit? No wonder the objects
<ce> were of no worth. I assume you put the fear of the gods into
<ce> him? That is consequence enough for me. Tell no guards.
<ce> Being caught will be punishment enough for one so young.
<ce> Fine work, good %pcf. ___qgiver_ thanks you.

RumorsDuringQuest:  [1005]
<ce> ___qgiver_ has a theft problem.

RumorsPostfailure:  [1006]
<ce> They say the Thieves Guild took over ___qgiver_.

RumorsPostsuccess:  [1007]
<ce> Some kid was doing the stealing all along.

QuestorPostsuccess:  [1008]
A warrior and a detective. Impressive.

QuestorPostfailure:  [1009]
Just today I received a report of another stolen item. For shame.

Message:  1011
<ce> Yeah, my _flam_ was stolen. I dunno what you're
<ce> asking me for. If I knew who, I'd just go over
<ce> and whap 'em over the head with my broom and be
<ce> done with it? Now are you done bothering me?

Message:  1012
<ce> Hello. Oh, my _cloth_? Have you found it?
<ce> Well, it's certainly not urgent. The thing was filled with
<ce> holes anyway. Another month and I couldn't even use it
<ce> as a rag anymore. I suppose it's just pride that's
<ce> keeping me curious now. If the thief actually needed it,
<ce> tell him to keep it. I should get over it already.

Message:  1020
_hum1_'s been furious about the stolen _flam_.
<--->
_hum1_'s _flam_ was stolen a few days ago.
<--->
Add _hum1_'s _flam_ to the pile of stolen goods.
<--->
If _hum1_ needs a new _flam_, she should ask that kid in _res4_ for one.

Message:  1021
I feel bad for _hum2_'s _cloth_.
<--->
I don't get the fuss about _hum2_'s stolen _cloth_. It's not like he misses it.
<--->
I saw someone cart off some _cloth_ near _res4_.

Message:  1022
_hum1_'s a good lady. Just a little ticked off about the theft.
<--->
I don't want to see anything bad happen to _hum1_ ever.

Message:  1023
_hum2_ doesn't care at all about his stolen stuff.
<--->
_hum2_'s been living in ___qgiver_ for several years.

Message:  1025
<ce> So soon as you mention the thefts,
<ce> this child turns pale as a sheet.
<ce> 
<ce> "Please don't tell anyone! I just wanted to try it!
<ce> I promise, I wasn't going to make any money from it!
<ce> I won't do it again! Please believe me!"
<ce> 
<ce> You spend several minutes ensuring that the boy returns the
<ce> stolen objects and gives up any lingering thought of trying
<ce> it again. _qgiver_ will want to hear of this.

Message:  1030
%qdt: 
 _qgiver_ has given me
 the task of finding a mysterious thief
 in ___qgiver_. I have two
 days to catch the culprit and return.

QBN:

Item _flam_ flamable anyInfo 1020
Item _cloth_ mens_clothing anyInfo 1021

Person _qgiver_ group Questor male
Person _hum1_ group Local_4.0 female anyInfo 1022
Person _hum2_ group Local_4.0 male anyInfo 1023
Person _thief_ faction Children male

Place _res1_ local tavern
Place _res2_ local generalstore
Place _res4_ local random

Clock _timer_ 2.00:00

--	Quest start-up:
	place npc _hum1_ at _res1_
	place npc _hum2_ at _res2_
	start timer _timer_
	log 1030 step 0
	add dialog for item _flam_
	add dialog for item _cloth_
	
_talk1_ task:
	clicked npc _hum1_
	say 1011
	add dialog for person _hum1_

_talk2_ task:
	clicked npc _hum2_
	say 1012
	add dialog for person _hum2_

_comb_ task:
	when _talk1_ and _talk2_
	dialog link for location _res4_ item _flam_
	dialog link for location _res4_ item _cloth_
	place npc _thief_ at _res4_

_kidtalk_ task:
	clicked npc _thief_
	say 1025

_pcgetsgold_ task:
	when _qgclicked_ and _kidtalk_ 
	give pc nothing
	add dialog for person _thief_
	end quest 

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when _qgclicked_ and not _kidtalk_
	clear _qgclicked_ _clearclick_

_timer_ task:
	say 1003
	end quest 