Quest: JHMC001
DisplayName: A Modest Proposal
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce> ERROR 1000 JHMC001

RefuseQuest:  [1001]
<ce> ERROR 1001 JHMC001

AcceptQuest:  [1002]
<ce> ERROR 1002 JHMC001

QuestFail:  [1003]
<ce> ERROR 1003 JHMC001

QuestComplete:  [1004]
<ce> _helpdb_ embraces _tele_,
<ce> the wizard who is able to cast the conveyance spell.
<ce> They spend only a few minutes catching up on what
<ce> has happened since _helpdb_ enlisted
<ce> your help, both knowing the underlying urgency
<ce> at hand. Finally, your ward turns to you
<ce> and bows deeply, taking the _tri_ back.
<ce> 
<ce> "_tele_, before you send me off,
<ce> could you please send this trinket off
<ce> into some pit in the Wrothgarian Mountains?
<ce> It needs to never see the light of day again."
<ce> 
<ce> The old man complies with a smile, and the
<ce> object disappears with a small flash.
<ce> 
<ce> "%pcf, how could I ever thank you enough?
<ce> I fear this will be the last time I ever speak to
<ce> you, for all elements of my past life must be forever
<ce> forgotten. But please, for your troubles, take this bit of
<ce> money. I can at least spare this much as a show of my unending
<ce> gratitude. Take it, and know that I will remember you forever."
<ce> _helpdb_ tosses you a bag full of money.
<ce>
<ce> The incantation begins and ends swiftly, and before you
<ce> even realize it, the young runaway is gone. The wizard
<ce> tips his hat to you kindly and makes his way out of
<ce> the building, leaving you alone.

Message:  1010
To the %ra named %pcn,
 
  Good day. I write to request your aid in a personal
matter of great importance. I am a child of the Dark
Brotherhood, inducted from birth, who has seen the
virtues of _temp_. I wish to absolve
the stains of my past. Will you help me come free?
For my own safety I cannot give my name in writing, but
I will stay in _res_ at ___res_
for a short period of time. Please come to me if you are
willing to help redeem a soul that feels lost.

Message:  1011
<ce> This person looks startled and swiftly
<ce> brandishes a weapon. Seeing you fail to
<ce> reciprocate, %g1 pauses and then relaxes.
<ce> 
<ce> "Oh, it must be you! That %ra! Your name...
<ce> %pcf, something like that? Yes, I've heard
<ce> plenty about you from a few fellows at the Fighters
<ce> Guild. Reliable, at least. And if you've come here
<ce> to help, that says you're moral enough to trust. My
<ce> name is _helpdb_, and I have to mention
<ce> that getting out of the Dark Brotherhood is ugly
<ce> business. I wish I could just march right to an abandoned
<ce> castle and live out my days in solitude, but if I were
<ce> that combat-ready, I wouldn't be asking for your help! I've
<ce> been given a new life in _temp_
<ce> that's put me in conflict with my Dark Brotherhood lines,
<ce> and I'm making the choice to give up my ties to my father
<ce> and everyone else. I want a new life, a new start, and
<ce> a new story. But now, let me ask you plainly.
<ce> Have you come to help me?"

Message:  1012
<ce> "%god be praised! And every other divine being in
<ce> our pantheon. You'd think someone of my rank would
<ce> be more familiar with them, but I once again
<ce> reiterate the dark past I'm trying to escape from.
<ce> Now, let me tell you a little about _db_.
<ce> He's one of the highest ranking Brotherhood officials
<ce> in %crn, and my father. To say he would be
<ce> disappointed in my choice would be an understatement.
<ce> Please watch your back until we part ways. If you
<ce> will, please take me to _thief_,
<ce> a member of the Thieves Guild in ___th_.
<ce> Apparently %g1 is excellent at assuming new
<ce> identities. Now then, let's be going."

Message:  1013
<ce> "By the way, let me give you this _tri_.
<ce> It's a family heirloom. Give it back to me
<ce> if you ever decide we should part ways.
<ce> If I'm found dead somewhere, you do not
<ce> want to have any connection to it, I promise
<ce> you. My father would never cease the hunt."

Message:  1015
<ce> _helpdb_ hands _thief_
<ce> a heavy bag of coins, and the two sit down for
<ce> quite a while to discuss plans for the future.
<ce> The expert thief explains the need to leave
<ce> %crn permanently, the importance of
<ce> mastering a new accent and new vocabulary
<ce> and learning a new trade thoroughly.
<ce> _thief_ explains some daily
<ce> exercises that will help change the
<ce> priest's manner of speech. _helpdb_
<ce> takes all the information in thoughtfully
<ce> and gives a final goodbye to this person
<ce> with the appearance of an old friend.
<ce> 
<ce> "Thank you for waiting, %pcf.
<ce> My days in %crn are limited, but I knew that
<ce> since I took my allegiance to _temp_
<ce> seriously. Now, I left a few personal
<ce> belongings with a friend in the temple
<ce> at ___temp_. That will be our next stop,
<ce> when we get around to it. I'll follow you."

Message:  1016
<ce> "My friend _pri_ should be here.
<ce> Let's go find %g2 to get my things."

Message:  1017
<ce> _helpdb_ is horrified at the
<ce> sight of _pri_'s body.
<ce> The wounds are very recent. The
<ce> nearby patrons wear a dread silence.
<ce> 
<ce> "And %g1 is carrying my things, too! They're
<ce> right here! What is the sense of all this?"
<ce> 
<ce> The door to the temple opens, and several footsteps
<ce> begin to sound. The answer to _helpdb_'s
<ce> question may be coming through the door.

Message:  1018
(The top of this letter has been intentionally cut off,
removing the recipients' names.)
 
  ...you to bring back one of my children, _helpdb_.
  Apparently obsessed with _temp_.
  Some %ra is babysitting %g2. Kill the bodyguard but do
  NOT hurt my family. Bring %g2 to me within =dummytimer_ days
  or your reward is forfeit. Mephala be with you.

              _db_
              _dbtown_
              ___dbtown_

Message:  1019
<ce> _helpdb_ looks somewhat shaken
<ce> by the experience inside the temple, though certainly
<ce> less than the ordinary priest would be.
<ce> 
<ce> "Thank you for getting us out of there. In the fracas
<ce> I managed to get my things off my poor friend's body. I
<ce> think this was all a trap to get us killed while I was
<ce> inspecting the body. Now, let us be going. I just
<ce> have a last place to go. Please get me to a house in
<ce> ___leave_, _leave_. I have a someone
<ce> there who can ferry me off to another party of the Iliac
<ce> Bay without a trace. I can finally start my new
<ce> life in the blink of an eye. I am anxious to go,
<ce> but we will follow your plan. 

Message:  1022
<ce> _helpdb_ snaps to attention as %g1
<ce> realizes where you have brought %g2. %g1 whispers.
<ce> 
<ce> "What are we doing here? Why here, of all places?"
<ce> 
<ce> You hold %g3 arm and drag %g2 tightly with you.

Message:  1023
<ce> _db_ smirks at the curious sight before him.
<ce> 
<ce> "Now this is interesting. The famed %ra
<ce> who has been escorting my momentarily confused
<ce> child, now in my very presence. The Night Mother
<ce> has dealt kindly with me, I see."
<ce> 
<ce> "Here is the simplest way for everyone to win.
<ce> I have no material reward, but in exchange for bringing
<ce> one of my errant heirs back into my grasp, I will
<ce> hold you in high esteem among the Brotherhood and
<ce> speak well of you to the appropriate people. The
<ce> favor of the Night Mother's children is something
<ce> no wise man takes lightly. All you must do is
<ce> tell me your name and give me my child back."
<ce> 
<ce> _helpdb_ is sweating and quaking.
<ce> This moment could define the rest of %g3 life.
<ce> Will you accept _db_'s offer?

Message:  1024
<ce> _db_ roars with laughter as
<ce> you push _helpdb_ toward him.
<ce> The next few seconds whir by.
<ce> _db_ topples _helpdb_
<ce> to the ground. Before %g1 can so much
<ce> as move, %g3 father uses a nearby
<ce> staff and swiftly cracks %g3 shins.
<ce> _helpdb_ wails in agony,
<ce> both from the tremendous pain and
<ce> your pitiless betrayal. The blows
<ce> continue, smashing an arm and some ribs
<ce> and other sundry parts of the body.
<ce> In mere moments your former client
<ce> is gasping for breath, inches
<ce> from death's door. _db_
<ce> leans down and speaks very slowly.
<ce> 
<ce> "You will never be free. Ever."
<ce> 
<ce> Satisfied, he reaches into his
<ce> cloak and pulls out a small gem. He
<ce> and _helpdb_ are consumed in
<ce> a burst of darkness, and you are left alone.

Message:  1025
<ce> _db_'s arms hang limp.
<ce> He is clearly unimpressed by your bravado.
<ce> 
<ce> "What a waste of my time. What did you
<ce> intend by coming here? Did you hope to kill
<ce> one of Mephala's own? Keep dreaming, %ra."
<ce> 
<ce> There is no time to react before he
<ce> pulls out a gem from his cloak. He
<ce> activates it and a burst of darkness
<ce> conveys him to some far-off place.
<ce> 
<ce> "Are you insane?!" _helpdb_ shouts.
<ce> "What kind of sneak attack was that?
<ce> I'm trying to give up the life of murder,
<ce> not compound it! Come on! Get us out of here!"

Message:  1026
<ce> _db_ shouts in horror. "NO!"
<ce> Your attempting to kill a member of his
<ce> family was clearly not a part of his plan.
<ce> Nevertheless, your surprise attack
<ce> found him unarmed and unable
<ce> to defend his child. He activates
<ce> some teleportation mechanism which
<ce> conveys him far away, though you
<ce> hear him sobbing as it takes effect.

Message:  1030
%qdt:
 A member of the Dark Brotherhood
 who wants to get out of the people-killing
 business has asked for my help to
 choose an entirely new path in life.
 I can find him or her in ___res_
 at _res_.

Message:  1031
%qdt:
 _helpdb_, who has asked for my
 help in leaving the Dark Brotherhood, wants me
 to accompany %g2 to ___th_
 to meet a member of the Thieves Guild,
 _thief_. I have also been
 given a _tri_ which I should return
 to _helpdb_ should I
 ever decide to part ways with %g2.

Message:  1032
%qdt:
 _helpdb_ next needs
 to pick up %g3 remaining belongings
 in _temp_ at ___temp_.

Message:  1033
%qdt:
 _helpdb_'s friend was slain,
 and we were attacked while in _temp_.
 The last destination is _leave_,
 a house in ___leave_.
 %g1 will be able to leave %crn
 without a trace if %g1 makes it there.

Message:  1075
Be very quiet about that. _db_ is staying in ___dbtown_.
<--->
Keep away from ___dbtown_ and you'll keep away from _db_. That's a secret.

Message:  1080
<ce> Will you instead slay _helpdb_?

Message:  1081
<ce> Your subtlety has caught %g2 by surprise.
<ce> The first blow is yours to make.

Message:  1082
<ce> "Well then, at least I can move on to
<ce> other offers to help. Thank you for
<ce> letting me know. Best of luck to you
<ce> over in the Fighters Guild."
<ce> 
<ce> _helpdb_ gestures politely
<ce> and heads out the door.

Message:  1085
<ce> You give _helpdb_ the _tri_.
<ce> Though %g1 looks disappointed, %g1 quickly
<ce> recovers and accepts that portion
<ce> of help you have been able to offer.
<ce> 
<ce> While %g3 guard is down, you could
<ce> get a first strike. Will you
<ce> attempt to slay _helpdb_?

Message:  1086
<ce> Bowing courteously, %g1 thanks you
<ce> for keeping %g2 safe so far
<ce> and continues on %g3 journey.

Message:  1087
<ce> "What? What are you doing?!"
<ce> _helpdb_ shrieks in
<ce> terror as you brace for combat.

Message:  1088
<ce> _helpdb_ is dead.
<ce> Your work here is finished.

Message:  1089
<ce> Though _helpdb_ will likely
<ce> be rattled by your departure, you
<ce> have spared yourself considerable trouble
<ce> by leaving %g2 in peace. Hopefully you
<ce> will never hear of this matter again.

QBN:

Person _helpdb_ group Local_4.2
Person _db_ faction Dark_Slayers male anyInfo 1075
Person _thief_ faction The_Thieves_Guild
Person _pri_ faction Generic_Temple
Person _tele_ faction The_Travelers_League male
Person _gendb_ faction The_Dark_Brotherhood

Place _res_ remote library
Place _temp_ remote temple
Place _th_ remote random
Place _dbtown_ remote house1
Place _leave_ remote house2

Clock _initial_ 60.00:00
Clock _killhelp_ 01:00

Item _tri_ trinket
Item _reward_ gold range 1001 to 1499
Item _body_ dead_body
Item _warning_ letter used 1018
Item _dummytimer_ gold range 6 to 15

Foe _helpdb1_ is Rogue
Foe _ambush_ is 3 Assassin
Foe _spawn_ is 2 Assassin

--	Quest start-up:
	start timer _initial_
    say 1010
    place npc _helpdb_ at _res_
    log 1030 step 0
    pc at _dbtown_ set _atdb_
    pc at _temp_ set _attemple_
    _tri_ used do _giveup_
    place npc _db_ at _dbtown_

_initial_ task:
    end quest

_begin_ task:
    clicked npc _helpdb_
    prompt 1011 yes _join_ no _refuse_
    stop timer _initial_

_join_ task:
    say 1012
    say 1013
    log 1031 step 1
    hide npc _helpdb_
    add _helpdb_ face
    create foe _spawn_ every 60 minutes indefinitely with 10% success
    place npc _thief_ at _th_
    get item _tri_
    add dialog for person _db_

_refuse_ task:
    prompt 1080 yes _murder_ no _fullrefuse_

_murder_ task:
    say 1081
    hide npc _helpdb_
    place foe _helpdb1_ at _res_
    start timer _killhelp_

_fullrefuse_ task:
    say 1082
    hide npc _helpdb_
    end quest

_killhelp_ task:
    say 1089
    restrain foe _helpdb1_
    end quest

_murderend_ task:
    killed _helpdb1_
    change repute with _gendb_ by -25
    say 1088
    end quest

_giveup_ task:
    prompt 1085 yes _strike_ no _peaceful_
    take _tri_ from pc
    drop _helpdb_ face

_peaceful_ task:
    say 1086
    end quest

_strike_ task:
    say 1087
    create foe _helpdb1_ every 0 minutes 1 times with 100% success

_thieftalk_ task:
    clicked npc _thief_
    say 1015
    log 1032 step 2
    place item _body_ at _temp_

_attemple_ task:
    say 1016

_bodytalk_ task:
    clicked item _body_
    say 1017
    create foe _ambush_ every 0 minutes 1 times with 100% success
    give item _warning_ to _ambush_
    take _body_ from pc

_nextturn_ task:
    when _bodytalk_ and not _attemple_
    say 1019
    log 1033 step 3
    place npc _tele_ at _leave_

_victory_ task:
    clicked npc _tele_
    give pc _reward_
    change repute with _gendb_ by -15
    hide npc _tele_
	end quest

_atdb_ task:
    say 1022

_strike2_ task:
    when _strike_ and _atdb_ and not _poorly_
    say 1026
    hide npc _db_
    change repute with _gendb_ by -25

_dbconv_ task:
    clicked npc _db_
    prompt 1023 yes _allin_ no _poorly_

_allin_ task:
    say 1024
    change repute with _gendb_ by +5
    hide npc _db_
    end quest

_poorly_ task:
    say 1025
    hide npc _db_
    change repute with _gendb_ by -25