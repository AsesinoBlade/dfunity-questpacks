Quest: JHFG004
DisplayName: Guard Duty
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          I've got another kind of job here, %pcf,
<ce>          in case you're tired of killing rats and bears.
<ce>        There's this house here in town that's afraid
<ce>          of a break-in tonight and they need a guard.
<ce>        It's paying =reward_ gold. Is that your kind of job?
<--->
<ce>        We got a client who needs _res_
<ce>        to get watched over tonight. Pretty simple job.
<ce>        Just get there by 11:00 tonight and stay there 'til
<ce>        sunlight. It's worth =reward_ gold. Wanna give it a shot?

RefuseQuest:  [1001]
<ce>          I hope I find someone quick. I don't
<ce>            want to have to cover this one.

AcceptQuest:  [1002]
<ce>                All right, good. Head over in town to
<ce>              _res_. Stay there from 
<ce>              11:00 at night to sunup. These people 
<ce>              weren't too specific about what threats
<ce>              they were expecting, but it's not gonna be
<ce>              vampires or werewolves or something, just
<ce>               some local low-life and his friends. Stay
<ce>                inside until they say they don't need you
<ce>              any longer. You're contracted for one night.
<ce>               They'll give you the gold when you're done.

QuestFail:  [1003]
<ce>               ERROR 1003 JHFG004

QuestComplete:  [1004]
<ce>           The sense of safety you brought was
<ce>           well worth the payment, %ra. This is
<ce>           what they said belonged to you.

Message:  1018
<ce>           Ah, our guard for the night! Please
<ce>       move about freely. We're under your care.
<--->
<ce>           Is that our guard for the night?
<ce>            Make yourself comfortable,
<ce>             wherever you please.

Message:  1020
<ce>                 We would have been killed
<ce>                  tonight if it weren't for you!
<ce>                    Thank you! Thank you!

Message:  1030
%qdt:
 The Fighters Guild in
 ___questgiver_ has assigned
 me to guard _res_
 tonight. I should be there from
 11:00 at night until they say
 otherwise, probably at dawn.

Message: 1099
<ce>       _res_, 23:00, tonight.
<ce>       Don't screw this one up.

QBN:
Item _reward_ gold range 300 to 500

Person _questgiver_ group Questor male

Place _res_ local house2

Clock _timer_ 1.06:00 0

Foe _enemy_ is 2 Burglar

--	Quest start-up:
	start timer _timer_ 
	log 1030 step 0 
	pick one of _E.01_ _E.02_
	pc at _res_ set _inside_

variable _E.01_
variable _E.02_

_inside_ task:
          start task _say_

_say_ task:
	say 1018

_spawntime_ task:
	daily from 2:00 to 4:00

_spawn_ task:
	when _E.01_ and _inside_ and _spawntime_
	create foe _enemy_ every 3 minutes 2 times with 100% success

_win_ task:
	killed 4 _enemy_
          start task _E.02_

_goaltime_ task:
	daily from 5:00 to 6:30

_notice_ task:
	when _E.01_ and _inside_ and _goaltime_
	say 1020

_pcgetsgold_ task:
	when _E.02_ and _inside_ and _goaltime_
	give pc _reward_ 
	end quest 

_qgclicked_ task:
	clicked npc _questgiver_ 

_clearclick_ task:
	when _qgclicked_
          say 1099
	clear _qgclicked_ _clearclick_ 

_timer_ task:
	end quest 