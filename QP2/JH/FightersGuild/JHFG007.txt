-- Fighters Guild quest
-- Req rep: 15+
Quest: JHFG007
DisplayName: Courier Work
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>          We have a job here that doesn't really
<ce>          fit the bill for what the Fighters Guild
<ce>          was made for, but it still pays. Do
<ce>          ya wanna go over to ___res_
<ce>          and bring some _pick_ someone left
<ce>          over there? Sounds like easy pickin's.
<--->
<ce>          We've got a request here to go over to
<ce>          ___res_ and pick up something
<ce>          that got left over there by accident.
<ce>          Sounds simple enough. Wanna take
<ce>          this one, %pcf?

RefuseQuest:  [1001]
<ce>         Man, are you just here to smash skulls or what?

AcceptQuest:  [1002]
<ce>             All right, get going then. It's gonna be in
<ce>             _res_. You're looking
<ce>             for some _pick_ that they said is
<ce>             gonna be on the floor. Just grab it and
<ce>             there should be some instructions on
<ce>             where you take it. That's what they
<ce>             told me, at least. _culp_
<ce>             should give you your reward after that.
<ce>             I'm giving you =timer_ days to do this.

QuestFail:  [1003]
<ce>           _questgiver_ expected you to return to
<ce>           ___questgiver_ with the _pick_ by now.

QuestComplete:  [1004]
<ce> This again? %oth. Hey, I'm really sorry
<ce> about this. We told the %t and everyone's doing
<ce> everything they can to find who's behind it.
<ce> But look, I'm not gonna let you walk away empty-handed.
<ce> Keep that _pick_ you got from them and sell
<ce> it if you want, and I'll give you =reward_ gold
<ce> out of my own pocket. It's not that much
<ce> but it's literally all I've got left over from
<ce> last week. Just know that we appreciate
<ce> how there are members like you watching
<ce> out for the rest of us.

Message:  1020
<ce> A faint snicker is all the warning you get
<ce> that something is about to go wrong.

Message:  1023
<ce> "Th-that's... im-mposs..."
<ce> The ringleader collapses.

Message:  1025
<ce>             You're getting some good results out there,
<ce>             _culp_. This month we're paying
<ce>             =sum_ for every dead mercenary and =bigsum_ 
<ce>             for every Fighters Guild bozo. Just bring the
<ce>             left hands this time. In a few more months
<ce>             they'll be all thinned out and then
<ce>             ___res_'ll be ours for the taking. Let
<ce>             that motivate you the next time some filthy
<ce>             %ra is trying to tackle you to the ground.
<ce>             
                                    K.

Message:  1030
%qdt:
 The Fighters Guild has given
 me a task which apparently
 doesn't involve fighting. I'm headed
 to ___res_ to pick up
 some _pick_ inside a building,
 _res_. They'll tell
 me where to take it after that.
 I should be back in ___questgiver_
 in =timer_ or fewer days.

Message:  1031
 %qdt:
 I was set up and violently ambushed.
 A note on the leader's body talks about
 a professional assassination scheme in
 ____res_. I don't have any
 more information, so I'll just
 head back to ___questgiver_ and
 tell _questgiver_ about it.

Message:  1099
<ce>        Getting cold feet, %pcf? It
<ce>        happens to everyone, I guess.
<ce>        But don't take too long. Get to
<ce>        ___res_ and back
<ce>        here before =timer_ days are up.

QBN:

Person _questgiver_ group Questor male
Person _culp_ group Local_3.0

Item _pick_ mineral
Item _reward_ gold range 60 to 140
Item _sum_ gold range 521 to 679
Item _bigsum_ gold range 835 to 999
Item _letter_ letter used 1025

Place _res_ remote house1

Clock _timer_ 00:00 0 flag 17 range 0 2

Foe _enemy_ is 5 Warrior
Foe _perp_ is Warrior

--   Quest start-up:
   start timer _timer_
   log 1030 step 0
   place item _pick_ at _res_   
   give item _letter_ to _perp_

_got_ task:
   clicked item _pick_
   say 1020
   place foe _perp_ at _res_
   create foe _enemy_ every 1 minutes 1 times with 100% success

_slain_ task:
   killed _perp_
   say 1023
   log 1031 step 1

_victory_ task:
  toting _pick_ and _questgiver_ clicked
  make _pick_ permanent
  give pc _reward_
  end quest

_qgclicked_ task:
    clicked npc _questgiver_

_clearclick_ task:
    when _qgclicked_ and not _slain_
    say 1099
    clear _qgclicked_ _clearclick_
 
_timer_ task:
    say 1003
    end quest