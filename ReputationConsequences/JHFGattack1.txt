Quest: JHFGattack1
DisplayName: Fighters Guild Attack 1
-- Message panels
QRC:

Message:  1010
<ce>  An angry roar shakes the ground.
<ce>  This time, it isn't a tiger or a bear.
<ce>  The worst kind of animal hunts you now:
<ce>  a lackey of the Fighters Guild.
<--->
<ce>  The Fighters Guild's mercenaries
<ce>  aren't skilled or quiet, but they
<ce>  are determined. One of them has
<ce>  found you, willing to turn in your
<ce>  corpse for a sizeable reward.

QBN:

Item _dummy_ gem

Foe _enemy1_ is Warrior

Clock _timer_ 1:00
Clock _longterm_ 21.00:00 35.00:00

--	Quest start-up:
    start timer _longterm_
    have _dummy_ set _clocker_

_longterm_ task:
    pick one of _happen_ _nothappen_
    
variable _nothappen_
_happen_ task:
    give pc _dummy_ notify 1010

_clocker_ task:
    start task _spawner_

_spawner_ task:
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    change foe _enemy1_ infighting true
    cast Troll's_Blood spell on _enemy1_
    cast Shield spell on _enemy1_
    cast Orc_Strength spell on _enemy1_
    cast Spell_Shield spell on _enemy1_
    start timer _timer_
    take _dummy_ from pc

variable _timer_

_mediumFGrep_ task:
    player faction-repute with-faction-id 41 is lower than -39

_majorFGrep_ task:
    player faction-repute with-faction-id 41 is lower than -69

_reset_ task:
    when _nothappen_ or _timer_ and _mediumFGrep_ and not _majorFGrep_
    start quest JHFGattack1
    end quest

_endthisquest_ task:
    when _nothappen_ or _timer_ and not _mediumFGrep_
    end quest

_escalate_ task:
    when _nothappen_ or _timer_ and _majorFGrep_
    end quest