Quest: JHMGattack1
DisplayName: Mages Guild Attack 1
-- Message panels
QRC:

Message:  1010
<ce>  A short distance from you, a hooded mage
<ce>  reaches for an object among his belongings.
<ce>  He activates an enchanted object, and
<ce>  multiple atronachs appear.
<ce>  
<ce>  "The Mages Guild is finished with you!"
<ce>  
<ce>  He teleports away, while his minions advance.
<--->
<ce>  A cloaked mage is concentrating on a long,
<ce>  laborious incantation. You arrive as it finishes.
<ce>  
<ce>  "Now, face the wrath of the Mages Guild!"
<ce>  
<ce>  Several hostile atronachs appear, and
<ce>  the caster makes use of an invisibility
<ce>  spell to get away.

QBN:

Item _dummy_ gem

Foe _enemy1_ is Iron_atronach
Foe _enemy2_ is Ice_atronach
Foe _enemy3_ is Fire_atronach

Clock _timer_ 1:00
Clock _longterm_ 21.00:00 35.00:00

--	Quest start-up:
    start timer _longterm_
    have _dummy_ set _clocker_

_longterm_ task:
    pick one of _happen_ _nothappen_
    
variable _nothappen_
_happen_ task:
    give pc _dummy_ notify 1010

_clocker_ task:
    start task _spawner_

_spawner_ task:
    create foe _enemy1_ every 0 minutes 1 times with 100% success
    create foe _enemy2_ every 0 minutes 1 times with 100% success
    create foe _enemy3_ every 0 minutes 1 times with 100% success
    change foe _enemy1_ infighting true
    change foe _enemy2_ infighting true
    change foe _enemy3_ infighting true
    start timer _timer_
    take _dummy_ from pc

variable _timer_

_mediumMGrep_ task:
    player faction-repute with-faction-id 40 is lower than -39

_majorMGrep_ task:
    player faction-repute with-faction-id 40 is lower than -69

_reset_ task:
    when _nothappen_ or _timer_ and _mediumMGrep_ and not _majorMGrep_
    start quest JHMGattack1
    end quest

_endthisquest_ task:
    when _nothappen_ or _timer_ and not _mediumMGrep_
    end quest

_escalate_ task:
    when _nothappen_ or _timer_ and _majorMGrep_
    end quest