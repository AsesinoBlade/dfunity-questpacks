Quest: garggem
DisplayName: Gargoyle Summoning Gem
-- Message panels
QRC:

Message:  1010
<ce> You are given a small amber and a receipt of purchase.
<ce> You note the date in your journal for reference.

Message:  1011
%qdt:
I bought a summoning amber for gargoyles today, which
is good for three castings for the next =timer_ days.

Message:  1012
<ce>  You activate the gem, and two mindless gargoyles
<ce>  appear out of thin air to defend you.
<ce>  This gem has two uses left.

Message:  1013
<ce>  You activate the gem, and two mindless gargoyles
<ce>  appear out of thin air to defend you.
<ce>  This gem has one use left.

Message:  1014
<ce>  You activate the gem, and two mindless gargoyles
<ce>  appear out of thin air to defend you.
<ce>  The gem crumbles to dust in your hand.

Message:  1015
<ce>  Two weeks are up, and the gargoyle summoning gem
<ce>  crumbles in your pack.

QBN:

Clock _timer_ 14.00:00
Clock _delay_ 0:01

Foe _summ1_ is Gargoyle
Foe _summ2_ is Gargoyle
Foe _summ3_ is Gargoyle
Foe _summ4_ is Gargoyle
Foe _summ5_ is Gargoyle
Foe _summ6_ is Gargoyle

Item _gem1_ amber
Item _gem2_ amber
Item _gem3_ amber

--   Quest start-up:
    say 1010
    get item _gem1_
    start timer _timer_
    log 1011 step 0
    _gem1_ used do _summon1_

_summon1_ task:
    say 1012
    create foe _summ1_ every 0 minutes 1 times with 100% success
    create foe _summ2_ every 0 minutes 1 times with 100% success
    change foe _summ1_ infighting true
    change foe _summ2_ infighting true
    change foe _summ1_ team 1
    change foe _summ2_ team 1
    take _gem1_ from pc
    get item _gem2_
    _gem2_ used do _summon2_

_summon2_ task:
    say 1013
    create foe _summ3_ every 0 minutes 1 times with 100% success
    create foe _summ4_ every 0 minutes 1 times with 100% success
    change foe _summ3_ infighting true
    change foe _summ4_ infighting true
    change foe _summ3_ team 1
    change foe _summ4_ team 1
    take _gem2_ from pc
    get item _gem3_
    _gem3_ used do _summon3_

_summon3_ task:
    say 1014
    create foe _summ5_ every 0 minutes 1 times with 100% success
    create foe _summ6_ every 0 minutes 1 times with 100% success
    change foe _summ5_ infighting true
    change foe _summ6_ infighting true
    change foe _summ5_ team 1
    change foe _summ6_ team 1
    take _gem3_ from pc
    start timer _delay_
    remove log step 0

_delay_ task:
    end quest

_timer_ task:
    say 1015
    end quest